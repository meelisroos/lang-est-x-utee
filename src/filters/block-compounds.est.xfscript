# block a compound word if:
# * its component belongs to a wrong word class

#
define WB [ "#" ];  # word border

define WW [ a | e | i | o | u | õ | ä | ö | ü ]; 
define CC [ b | d | f | g | h | j | k | l | m | n | p | r | s | t | v ] ;
#define ZZ [[W C+ W+ C*] | [W W W C*]];   # at least 2 syllables (kind of...)

define Nmber ["+Sg" | "+Pl" ];

define ShortAdj [CC* WW* CC* WW+ CC* "+A"] ; # short adjective (max 2 syllables)
define ShortAdv [CC* WW* CC* WW+ CC* i* "+Adv"] ; # short adverb (max 2 syllables (+ i))
define GoodAdv1 [[{kallale} | {kaugele} | {lähedal} | {lähedale} | 
                  {lähedalt} | {segamini} | {vahele} | {üleval}] "+Adv"]; # they suit well for 1st parts of V-der compounds

# derivation
# V -> 

define vderneedspar [  "+Der/v" | "+Der/tav" | "+Der/mata" | "+Der/matu" | 
                     "+Der/nud" | "+Der/nu" | "+Der/tu" | "+Der/tud"] ;   # suftyyp 19, 20
define vderneedsgen ["+Der/mine" | "+Der/ja" | "+Der/us" | "+Der/mus" | "+Der/ng" ] ; # suftyyp 18

define AnyDer ["+Der/mine" | "+Der/ja" | "+Der/nu" | "+Der/mus" | "+Der/ng" | "+Der/v" | "+Der/tav" | 
                "+Der/nud" | "+Der/mata" | "+Der/matu" | "+Der/tamatu" | "+Der/tu" | "+Der/tud" | 
                 "+Der/us" | "+Der/lik" | "+Der/line" | "+Der/lt" | "+Der/sti" | "+Der/m" | "+Der/im" | 
                "+Der/nna" | "+Der/kond" | "+Der/ti" | "+Der/lane" | "+Der/minus" ] ;

define SomePron [ {ise} | {kes} | {mis} | {see} | {too} | {mina} | {sina} | {meie} | {teie} ];

# nouns meaning an action
define DeVerbal [ {elu} | {hüpe} | {hüüatus} | {hüüd} | {hüüe} | {jooks} | {kalle} | {kutse} | 
                 {käik} | {külv} | {künd} | {lask} | {lend} | {lõige} | {löök} | {lüps} | 
                 {matk} | {minek} | {olek} | {olija} | {panek} | {proov} | {põige} | {põrge} | 
                 {pääs} | {püüd} | {püüe} | {ränd} | {rännak} | {ränne} | {seis} | {start} | 
                 {sõit} | {tants} | {teke} | {trükk} | {tulek} | {tung} | {tõmme} | {tõrje} | 
                {tõuge} | {tõus} | {vaade} | {vedu} | {vise} | {võtt} ] ;

# not suitable for compounding
define BadInterj [ {ena} | {pele} | {raip} | {vaat} | {paganamas} | {servus} | {kurat} ] ;

define GoodAdj1 [ {kõlblik} | {kõlbulik} | {ohtlik} | {tundlik} | {suutlik} | {kõlbmatu} | {suutmatu} |
                  {andekas} | {nõudlik} | {võimekas} | {osav} | {tihe} | {kindel} ] ;

# indecl adjectives
define GoodAdj2 [ {karva} | {kasvu} | {moodi} | {värvi} | {väärt} | {korras} ] ;

# as a rule, short words make weird compounds...
define GoodShort [ {aeg} | {elu} | {maa} | {töö} | {õde} ] ;

# some wordclasses are never in compounds
define BadEver [ "+Adp" ] ;
define BadFirst [ "+V" \[WB | AnyDer | "+Inf" | "+Sup"]* WB] ;
define BadLast [WB \WB* ["+Pron" | "+Interj" | "+CC" | "+CS"]];
define BadCmp $[BadEver | BadFirst | BadLast];

# a verb cannot be the last part of a compound, unless it is a derivation;
# and a derivation requires the previous part in a certain form;
# the good constellations are negated below
 
define BadVerb [ 
    $[ WB ?* "+V" ] & 
   ~$[ "+N" "+Sg" "+Nom" \[AnyDer]* WB ?* "+V" [vderneedspar | vderneedsgen] ] &   # lauduhmaja
   ~[ ShortAdj (["+Comp" | "+Superl"]) "+Sg" "+Nom" \[AnyDer]* WB ?* "+V" [vderneedspar | vderneedsgen] ?*] &   # rämeuhmaja, enimuhmav
   ~[ ShortAdv \[AnyDer]* WB ?* "+V" [vderneedspar | vderneedsgen] ?*] &          # huupiuhmaja
   ~[ GoodAdv1 \[AnyDer]* WB ?* "+V" [vderneedspar | vderneedsgen] ?*] &          # vaheleuhmaja
   ~$[ "+N" Nmber \["+Nom" | "+Par"] \[AnyDer]* WB ?* "+V" vderneedsgen ] &       # lauauhmaja
   ~$[ "+N" Nmber \["+Nom" | "+Gen"] \[AnyDer]* WB ?* "+V" vderneedspar ] &       # laudauhmav
   ~$[ "+Num" "+Card" Nmber [ "+Gen" | "+Par"] \[AnyDer]* WB ?* "+V" vderneedsgen ] &   # viiekirjutamine, viiskirjutamine
   ~$[ "+V" \[AnyDer]* ["+Inf" | "+Sup"] \[AnyDer]*  WB ?* "+V" [vderneedspar | "+Der/mine" | "+Der/ja"]] & # lugedaunistav, lugemaõppimatu, ...
   ~$[ {ne} "+A" "+Der/minus" WB ?* "+V" [vderneedspar | vderneedsgen] ] &     # astraaluhmamine, astraaluhmav
   ~$[ "+N" "+Der/minus" WB ?* "+V" vderneedsgen ] &                           # vaatamisuhmamine, firenzelasuhmaja
   ~[ SomePron "+Pron" Nmber "+Gen" \[AnyDer]* WB ?* "+V" vderneedsgen ?*] &   # eneseuhmaja
   ~[ SomePron "+Pron" Nmber "+Par" \[AnyDer]* WB ?* "+V" vderneedspar ?*] &   # ennastuhmav
   ~[ {kõik} "+Pron" "+Pl" "+Par" \[AnyDer]* WB ?* "+V" vderneedspar ?*]       # kõikiuhmav
                ];  # NB! not finished

define BadNoun [
    $[ WB \[WB | "+V"]* "+N" ] & 
   ~$["+N" "+Sg" "+Nom" \[AnyDer]*  WB \WB* "+N"] &               # laudriiul
   ~[ ShortAdj "+Sg" "+Nom" \[AnyDer]* WB \WB* "+N" ?*] &            # rämeriiul
   ~$["+N" Nmber "+Gen" \[AnyDer]*  WB \WB* "+N"] &               # lauariiul, laudaderiiul
   ~$["+N" "+Sg" \[AnyDer]*  WB DeVerbal "+N"] &             # mäkketulek, mäesttulek, ...
   ~$[ "+Num" "+Card" Nmber "+Gen" \[AnyDer]* WB \WB* "+N" ] &        # viieriiul
   ~$[ "+Num" "+Card" "+Sg" "+Nom" \[AnyDer]* WB \WB* "+N" ] &        # viisriiul
   ~$["+V" \[AnyDer]* "+Sup" \[AnyDer]*  WB DeVerbal "+N"] &    # lugematulek
    ~[ ShortAdv \[AnyDer]* WB DeVerbal "+N" ?*] &      # huupitulek
   ~$[ {ne} "+A" "+Der/minus" WB \WB* "+N" ] &                     # astraalriiul
   ~$[ "+N" "+Der/minus" WB \WB* "+N" ] &                          # vaatamisriiul
   ~[ SomePron "+Pron" Nmber "+Gen" \[AnyDer]* WB \WB* "+N" ?*]     # eneseriiul
               ];

define BadAdj [
    $[ WB \[WB | "+V"]* "+A" ] & 
   ~$[\[AnyDer]* "+N" "+Sg" "+Nom" \[AnyDer]*  WB \WB* "+A"] &               # laudpunane
   ~[ ShortAdj "+Sg" "+Nom" \[AnyDer]* WB \WB* "+A" ?*] &            # rämeastraalne
   ~$["+N" Nmber "+Gen" \[AnyDer]*  WB \WB* \v "+A"] &               # lauapunane, laudadepunane
   ~$["+N" "+Sg" \["+Nom" | "+Gen" | AnyDer]*  WB \WB* v "+A"] &             # mägejuhtiv, mäesttulev, ...
   ~$[ "+Num" "+Card" Nmber "+Gen" \[AnyDer]* WB \WB* {ne} "+A" ] &        # viiemeetrine
   ~$[ "+Der/minus" WB \WB* "+A" ] &                          # vaatamispunane, astraalpunane
   ~[ SomePron "+Pron" Nmber "+Gen" \[AnyDer]* WB \WB* "+A" ?*]     # enesepunane
               ];

define BadAdv [
    $[ WB \[WB | AnyDer]* "+Adv" \[AnyDer]* ] 
               ];

define BadNum [
    $[ WB \[WB | AnyDer]* "+Num" \[AnyDer]* ] 
               ];

regex  ~[ BadCmp | BadVerb | BadNoun | BadAdj | BadAdv | BadNum] ;




