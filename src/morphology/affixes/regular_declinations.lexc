! inflectional types and analogue rules for nouns, adjectives and other declinables
! VVS stands for types from Ü. Viks "Väike vormisõnastik" for reference purposes

!---------------
! inflectional types
!---------------

!---------------
! not-so-exceptional inflectional types
!---------------

LEXICON AASTA    ! VVS 1 (productive) for vowel-final; stress on penultimate syllable words

              : SG_NOMINATIVE ;    ! aasta
              : 2C ;    ! aasta etc
              : US_DERIVATION ;
              : LIK_DERIVATION ;

LEXICON REDEL    ! VVS 2 (productive) for consonant-ending, stress on penultimate syllable words
   
              : SG_NOMINATIVE ;    ! redel, koppel
              :i 2C ;    ! redeli etc
              : US_DERIVATION ;
              : LIK_DERIVATION ;

LEXICON VIRSIK    ! VVS 2 (productive) for -ik, -ing; stress on penultimate syllable words

              : SG_NOMINATIVE ;    ! virsik
              :u 2C ;    ! virsiku etc
              : LIK_DERIVATION ;

LEXICON LOHAKAS    ! VVS 2 (productive) -kas, -jas etc ending words (incl derivations)

              :s SG_NOMINATIVE ;    ! lohakas
              : 2C ;    ! lohaka etc
              : US_DERIVATION ;
              : LIK_DERIVATION ;   ! kaabakalik ???

LEXICON TUNTUD    ! VVS 2 (productive) -d ending words (incl derivations); only adjectives

              :d SG_NOMINATIVE ;    ! tuntud
              : 2C ;    ! tuntu etc
              : US_DERIVATION ;
 
LEXICON MAGUS    ! VVS 2 (productive) for -v, -tav derivations (adjectives)
                 ! + (unproductive) i.e. unstable (closed) type

              : SG_NOMINATIVE ;    ! magus
              :A1 2C ;    ! magusa; A1 is necessary for pingne - pingsa
              :A1 US_DERIVATION ;

LEXICON HALE    ! VVS 2 (unproductive) i.e. unstable (closed) type

              : SG_NOMINATIVE ;    ! hale, sajas
              :da 2C ;    ! haleda, sajanda
              :da US_DERIVATION ;

LEXICON HOOLAS    ! VVS 3 (unproductive) i.e. unstable (closed) type; strengthening gradation

              :%{W%} SG_NOMINATIVE ;        ! hoolas
              :a 2C ;                    ! hoolsa etc
   %+Use%/Rare:%{W%}%>t SG_PARTITIVE ;      ! hoolast
%+Use%/NotNorm:%{W%}%>te N_PL_SUBPARA ;  ! hoolaste etc 
              :a US_DERIVATION ;

LEXICON HAARE    ! VVS 4, VVS 5 (unproductive) i.e. unstable (closed) type

              :%{W%} 2C_LEMMA2 ;         ! haare, haaret
              :me 2C_STRONGPL ;          ! haarme, habeme etc, haarmeid, haarmeil etc
              :me LIK_DERIVATION ;

LEXICON PINGE    ! VVS 6 (productive) i.e. stable 
                 ! (open to verbal derivations, e.g. kiune from kiunuma)
                 ! + (unproductive) i.e. unstable (closed) type; 

              :%{W%} 2C_LEMMA3 ;          ! pinge, pinget, pingete
              : 2C_STRONG ;               ! pinge etc, pingeid, pingeil etc
              : US_DERIVATION ;
              : LIK_DERIVATION ;

LEXICON KOTKAS    !  VVS 7 (unproductive) i.e. unstable (closed) type; 

              :s%{W%} 2C_LEMMA3 ;         ! kotkas, kotkast, kotkaste
              : 2C_STRONG ;               ! kotka etc, kotkaid, kotkail etc
              : US_DERIVATION ;
              : LIK_DERIVATION ;

LEXICON PIPAR    !  VVS 8 (unproductive) i.e. unstable (closed) type; = HOOLAS

              :%{W%} 2C_LEMMA3 ;          ! pipar, pipart, piparde
              :a 2C_STRONG ;
%+Use%/NotNorm:a%>te N_PL_SUBPARA ;       ! piprate etc 
              :a LIK_DERIVATION ;

LEXICON SOOLANE    ! VVS 9 (productive) simplex words ending with -s (katus), VVS 10 affix -ne; 
                       
              : SG_NOMINATIVE ;
              : NE_S_COMMON ;
              :E1%>sse SG_ILLATIVE ;            ! soolasesse, katusesse
              :E1%>id PL_PARTITIVE ;            ! soolaseid, katuseid
   %+Use%/Rare:E1%>i N_VOWEL_PL_SUBPARA ;    ! soolaseil, katuseil etc
              :E1»i IM_DERIVATION ;            ! soolaseim

LEXICON OLULINE    ! VVS 11, 12 (productive)
                   ! VVS 12: affixes -lane, -line, -mine, -tine, -ke(ne), -stikune (?)
                   ! VVS 11: derived words ending with -us, -is (?) (harjutus)
                  
              : SG_NOMINATIVE ;
              : NE_S_COMMON ;
              : LINE_US_SPECIFIC ;
              :E1 LIK_DERIVATION ;

LEXICON KATKINE    ! VVS 9 & (11), 10 & (12) (productive)
                   ! VVS 10 & (12): affix -ne, but sg ill, pl part and vocal cases also like -line
                   ! VVS  9 & (11): simplex -s, but also like -us (gloobus) 
                 
              : SOOLANE ;
   %+Use%/Rare: LINE_US_NORM ;              ! like OLULINE: katkisse, katkisi
   %+Use%/Rare:%>I1 N_VOWEL_PL_SUBPARA ;    ! like OLULINE: katkisil etc; (ever used?)
              :E1 LIK_DERIVATION ;

LEXICON SUULINE    ! VVS (9) & 11, (10) & 12 (productive)
                   ! VVS (10) & 12: affix -line, but pl part and vowel cases also like -ne
                   ! VVS  (9) & 11: derived -us, but pl part and vowel cases (?) also like simplex -s (vaatlus) 

              : OLULINE ;
   %+Use%/Rare:E1%>id PL_PARTITIVE ;            ! like SOOLANE: suuliseid, vaatluseid
   %+Use%/Rare:E1%>i N_VOWEL_PL_SUBPARA ;    ! like SOOLANE: suuliseil, vaatluseil etc
   %+Use%/Rare:E1»i IM_DERIVATION ;            ! like SOOLANE: suuliseim
              :E1 LIK_DERIVATION ;

LEXICON PEENIKE    ! VVS 12 (productive): affix -ke; almost = OLULINE, except sg nom

              : SG_NOMINATIVE ;
   %+Use%/Rare:ne SG_NOMINATIVE ;
              :ne NE_S_COMMON ;
              :ne LINE_US_SPECIFIC ;
              :E1 LIK_DERIVATION ;

LEXICON PERE    ! VVS 16 (productive)

              : SG_NOMINATIVE ;
              : N_SG_SUBPARA ;          ! pere, baika etc
              :%>t SG_PARTITIVE ;        ! peret, baikat
              :%{S%} SG_ILL_VARIANT ;     ! perre
              : SG_ILL_VARIANT ;         ! peresse, baikasse 
              : DE_PL ;
              : LIK_DERIVATION ;

LEXICON KAVA    ! VVS 17, 18 (productive) + (unproductive)
                ! VVS 17 (productive)
                ! VVS 18 (unproductive): words with quality gradation

              : SG_NOMINATIVE ;
              :%{W%} N_SG_SUBPARA ;     ! kava, sõja etc
              : SG_PARTITIVE ;           ! kava
              :%{S%} SG_ILL_VARIANT ;     ! kavva
              :%{W%} SG_ILL_VARIANT ;    ! kavasse 
              : DE_PL ;
              : LIK_DERIVATION ;

LEXICON MUUSEUM    ! VVS 2 & 19 (productive)
                   ! forms may be based on 2 productive, i.e. stable types (aktiivne muuttüüp)
                   ! if prononunced as 2-syllable word (muus-jum), then like REDEL
                   ! if prononunced as 3-syllable word (muus-e-um), then like TAUD 

              : SG_NOMINATIVE ;
              :i%{W%} N_SG_SUBPARA ; 
              :i SG_PARTITIVE ;             ! muuseumi
   %+Use%/Rare:i SG_ILLATIVE ;              ! muuseumi  ! in TAUD, this is the default ill
              :i%{W%}%>sse SG_ILLATIVE ;    ! muuseumisse ! in TAUD, this is the rare ill
              :i%>de N_PL_SUBPARA ;      ! muuseumide etc 
              :e PL_PARTITIVE ;             ! muuseume

   %+Use%/Rare:i%>t SG_PARTITIVE ;          ! muuseumit
   %+Use%/Rare:i%>te N_PL_SUBPARA ;      ! muuseumite etc 
   %+Use%/Rare:e%>id PL_PARTITIVE ;         ! muuseumeid
   %+Use%/Rare:e%>i N_VOWEL_PL_SUBPARA ; ! muuseumeil etc
              : LIK_DERIVATION ;

LEXICON PIIM    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) sepp, kaup, ...

              : SG_NOMINATIVE ;
              :%{sg.a%} 1C ;
              :a US_DERIVATION ;
              : LIK_DERIVATION ;          ! NB! not checked on real usage
              :a%{W%} LIK_DERIVATION ;         ! NB! not checked on real usage

LEXICON KOON    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) lõpp, laug

              : SG_NOMINATIVE ;
              :%{sg.u%} 1C ;
              : LIK_DERIVATION ;          ! NB! not checked on real usage
              :u%{W%} LIK_DERIVATION ;         ! NB! not checked on real usage

LEXICON EIT    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) kuusk, kärg

              : SG_NOMINATIVE ;
              :%{sg.e%} 1C ;
              : LIK_DERIVATION ;          ! NB! not checked on real usage
              :e%{W%} LIK_DERIVATION ;         ! NB! not checked on real usage

LEXICON TAUD    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) siil, president, ...

              : SG_NOMINATIVE ;
              :%>%{pl.e%}%{sg.i%} 1C ;    ! {pl.e} is here so it need not be in the stem lexicon
              : LIK_DERIVATION ;          ! NB! not checked on real usage
              :i%{W%} LIK_DERIVATION ;         ! NB! not checked on real usage

LEXICON FIAT    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) 
                ! almost == TAUD, but final stop weak grade does not change 

              : SG_NOMINATIVE ;
              :i%{WL%} N_SG_SUBPARA ;      ! Fiati etc
              :i SG_PARTITIVE ;               ! Fiati
              :i SG_ILLATIVE ;                ! Fiati
    %+Use%/Hyp:i%{WL%}%>sse SG_ILLATIVE ;     ! Fiatisse
              :%>%{pl.e%}%{sg.i%} DE_PL ;  ! Fiatide, Fiate, Fiatisid+Use/Rare 
              : LIK_DERIVATION ;          ! ?? lowercasing also ??
              :i%{WL%} LIK_DERIVATION ;          ! ?? lowercasing also ??

LEXICON KABINET    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) 
                   ! almost == TAUD, but Sg Nom orthography with single final stop instead of 2

              : SG_NOMINATIVE ;    ! kabinet
                                          ! sg nom has a single final stop instead of double, 
                                          ! so it has to be doubled to get the forms right
              :%{dbl%}%>%{pl.e%}%{sg.i%} 1C ;
              : LIK_DERIVATION ;          ! NB! not checked on real usage
              :i%{W%} LIK_DERIVATION ;         ! NB! not checked on real usage

! VVS 24 is lexical variants under VVS 22 various subtypes

LEXICON ELANIK    ! VVS 25; (productive) i.e. stable type (aktiivne muuttüüp) 
                  ! almost == KABINET == TAUD

              : SG_NOMINATIVE ;    ! elanik
                                          ! sg nom has a single final stop instead of double, 
                                          ! so it has to be doubled to get the forms right
              :%{dbl%}%{sg.u%} 1C_SG ;         ! elaniku etc
              :%{dbl%}e%{W%} N_PL_SUBPARA ;    ! NB! elanike etc
   %+Use%/Rare:%{dbl%}%{sg.u%}%>de N_PL_SUBPARA ;    ! elanikkude etc   
              :%{dbl%}%>%{pl.e%}%{sg.u%}%>%{s%}%{i%}%{d%} PL_PAR_VARIANT ;    ! elanikke; elanikkusid+Use/Rare
              :%{dbl%} US_DERIVATION ;            ! õnnelikkus
              :%{dbl%}e%{W%}» IM_DERIVATION ;    ! õnnelikem
              :u%{W%} LIK_DERIVATION ;

LEXICON ARMEE    ! VVS 26 ; (productive) i.e. stable type (aktiivne muuttüüp)

              : 1V ;
              :%>sse SG_ILLATIVE ;    ! armeesse
              : LIK_DERIVATION ;

! ---------------
! sub-paradigms common to several inflectional types
! ---------------

LEXICON NE_S_COMMON ! VVS 9, 10, 11, 12 common forms for affix -ne, -line, -s, -tus etc 

              :E1 N_SG_SUBPARA ;                
              :E1%>t SG_PARTITIVE ;         ! soolast, raudset, olulist
              :E1%>te N_PL_SUBPARA ;        
              :E1 US_DERIVATION ;
              :E1 SHORT_NE_STEM ;           ! nais-, vaatamis-, astraal-

LEXICON LINE_US_SPECIFIC   ! -line, -us ending words
              : LINE_US_NORM ;
              : LINE_US_RARE ;

LEXICON LINE_US_RARE  ! part of line_us_specific

   %+Use%/Rare:E1%>sse SG_ILLATIVE ;          ! olulisesse, harjutusesse
   %+Use%/Rare:%>I1 N_VOWEL_PL_SUBPARA ;   ! olulisil, harjutusil etc

LEXICON LINE_US_NORM  ! part of line_us_specific

              :E1%>se SG_ILLATIVE ;           ! olulisse, harjutusse (haplology)
              :%>I1 PL_PARTITIVE ;            ! olulisi, harjutusi
              :»I1 IM_DERIVATION ;           ! olulisim
      
! ------

LEXICON 1C    ! consonant ending, stress NOT on penultimate syllable

                           ! NOMINATIVE is without stem vowel
              : 1C_SG ;    ! singular forms + pl nom
              : DE_PL ;

LEXICON 1V    ! vowel ending, stress on the last syllable

              : SG_NOMINATIVE ;    ! armee
              : N_SG_SUBPARA ;            ! armee etc
              :%>d SG_PARTITIVE ;            ! armeed
              :%>de N_PL_SUBPARA ;        ! armeede etc  
              :%>%{s%}id PL_PAR_VARIANT ; ! armeesid; armeid...

LEXICON 2C    ! stress on penultimate syllable & word ends with a consonant; 
              ! or stressed penult. syllable extra heavy (e.g. -aania)  

                                          ! NOMINATIVE is without stem vowel
              :%>t SG_PARTITIVE ;            ! redelit, virsikut
              : 2C_STRONGPL ;             ! redeleid, redeleil etc

! in addition to Sg NOMINATIVE + 2C,
! a strengthening gradation paradigm may consist of either:
! 2C_LEMMA2 + 2C_STRONGPL, or 
! 2C_LEMMA3 + 2C_STRONG

LEXICON 2C_LEMMA2    ! subparadigm where 2 stems = lemma  

              : SG_NOMINATIVE ;              ! ase, haare
              :%>t SG_PARTITIVE ;            ! aset, haaret

LEXICON 2C_LEMMA3    ! subparadigm where 3 stems = lemma  

              : SG_NOMINATIVE ;              ! pinge
              :%>t SG_PARTITIVE ;            ! pinget
              :%>te N_PL_SUBPARA ;        ! pingete etc

LEXICON 2C_STRONG    ! forms that would usually be in strong grade in strengthening gradation

              : N_SG_SUBPARA ;             ! redeli, kotka etc
              : SG_ILL_VARIANT ;           ! taskusse or tasku
              :%>id PL_PARTITIVE ;            ! kotkaid
   %+Use%/Rare:%>i N_VOWEL_PL_SUBPARA ;    ! kotkail etc
              :»i IM_DERIVATION ;         ! magusaim, rentaableim, ...

LEXICON 2C_STRONGPL   ! forms that would sometimes be in strong grade in strengthening gradation

              : 2C_STRONG;                ! aseme, haarme etc
              :%>te N_PL_SUBPARA ;        ! asemete, haarmete

! LEXICON 2V    ! stress on penultimate syllable, word ends with vowel 
!              ! & stressed syllable NOT extra heavy (kava, sõda, pere)
!
!              :%{WL%} N_SG_SUBPARA ;    ! kava, sõja etc
!                                        ! PARTITIVE is +0 or +t
!              : 2V_ILLATIVE ;
!              : DE_PL ;

LEXICON 1C_SG    ! SG subpara of 1C

              :%{W%} N_SG_SUBPARA ;    ! koti etc
              : SG_PARTITIVE ;            ! kotti
              : 1C_ILLATIVE ;          ! kotti, kotisse

LEXICON DE_PL    ! PL subpara of TAUD, KAVA, PERE

              :%>de N_PL_SUBPARA ;     ! kottide etc
                                       ! pl par stem vowel (u, i, 0) is often lexically determined 
                                       ! and usage-wise tagged
              :%>%{s%}%{i%}%{d%} PL_PAR_VARIANT ;   ! kingi, kingasid+Use/Rare vs. puudi+Use/Rare,
                                                    ! puudasid; kavu, kavasid; sõdu, sõdasid

! ---------------
! rules of analogy
! (cases grouped in various ways, for combining in different paradigms)
! ---------------

LEXICON N_SG_SUBPARA    ! sg gen + sg cases depending on it + pl nom; excl sg ill

       +Pl+Nom:%>d PL_NOM_GI ;
              : SG_GEN_ET_AL ;

LEXICON N_PL_SUBPARA    ! pl gen + pl cases depending on it 

           +Pl: STANDARD_GEN_ET_AL ;

LEXICON STANDARD_GEN_ET_AL    ! genitive and cases depending on it (either sg or pl)
                              ! incl. sse-illative 
              : GENITIVE ; 
              : STANDARD_SEM_CASES ;

LEXICON SG_GEN_ET_AL          ! sg genitive and sg cases depending on it (either sg or pl)
                              ! NB! excl. sg illative 
              : SG_GENITIVE ; 
           +Sg: REGULAR_SEM_CASES ;

LEXICON STANDARD_SEM_CASES    ! semantic cases, incl sse-illative

              :%>sse ILLATIVE ;
              : REGULAR_SEM_CASES ;

LEXICON REGULAR_SEM_CASES    ! semantic cases, excl illative

              : REGULAR_LOCATIVES ;
              :%>ks TRANSLATIVE ;
              : NINATAGA ;

LEXICON NINATAGA

              :%>ni TERMINATIVE ;	
              :%>na ESSIVE ;	
              :%>ta ABESSIVE ;	
              :%>ga COMITATIVE ;

	
LEXICON N_VOWEL_PL_SUBPARA    ! pl cases depending on vowel plural gen  
                              ! excl. ni-na-ta-ga 
           +Pl: LOCATIVES ;

LEXICON LOCATIVES            

              : STANDARD_LOCATIVES ;
              :%>ks TRANSLATIVE ;

LEXICON STANDARD_LOCATIVES            

              :%>sse ILLATIVE ;
              : REGULAR_LOCATIVES ;

LEXICON REGULAR_LOCATIVES            

              :%>s INESSIVE ;
              :%>st ELATIVE ;
              :%>le ALLATIVE ;	
              :%>l ADESSIVE ;	
              :%>lt ABLATIVE ;	

! -----------
! individual cases
! -----------

LEXICON 1C_ILLATIVE       ! sse-illative is very rare;
              : SG_ILLATIVE ;              ! kotti
    %+Use%/Hyp:%{W%}%>sse SG_ILLATIVE ;    ! kotisse

! LEXICON 2V_ILLATIVE       ! sse-illative is the default; few words have the short form also
!              :%{S%} SG_ILL_VARIANT ;     ! kavva
!              :%{WL%} SG_ILL_VARIANT ;    ! kavasse 

! the preferred form for Pl Par and Sg Ill is indicated in the stem lexicon
! (not in the continuation lexicon)
! so these variants allow us to carry the info from stem lex to the final surface form

LEXICON PL_PAR_VARIANT    ! for TAUD, PIIM, KOON etc types

   %+Use%/Rare:%{rare%} PL_PARTITIVE ;    ! less used pl par form for that word
              : PL_PARTITIVE ;            ! default pl par form for that word

LEXICON SG_ILL_VARIANT    ! for KEEL etc types

   %+Use%/Rare:%>%{s%}%{s%}%{e%}%{rare%} SG_ILLATIVE ;  ! less used sg ill form for that word
              :%>%{s%}%{s%}%{e%} SG_ILLATIVE ;          ! default sg ill form for that word

! -------- these cases are (often) non-predictable from Sg Nom

LEXICON SG_NOMINATIVE
           +Sg:%{no.apo%} NOMINATIVE ;
              : LANE_DERIVATION ;

LEXICON SG_GENITIVE
           +Sg: GENITIVE ;
              : KE_DERIVATION ;
              : LINE_DERIVATION ;
              : M_DERIVATION ;
              : LT_DERIVATION ;
              : STI_DERIVATION ;
              : INI_DERIVATION ;
              : TI_DERIVATION ;
              : NNA_DERIVATION ;

LEXICON SG_PARTITIVE
           +Sg: PARTITIVE ;

LEXICON SG_ILLATIVE
           +Sg: ILLATIVE ;

LEXICON PL_PARTITIVE
           +Pl: PARTITIVE ;

! --------- the 14 individual cases ------------
!! flag diacritics can be used for remembering the case 

LEXICON NOMINATIVE
          +Nom: NOM_GI ;

LEXICON GENITIVE
          +Gen: GEN_GI ;

LEXICON PARTITIVE
          +Par: PAR_GI ;	

LEXICON ILLATIVE
          +Ill: SEM_GI ;	

LEXICON INESSIVE
          +Ine: SEM_GI ;	

LEXICON ELATIVE
          +Ela: SEM_GI ;	

LEXICON ALLATIVE
          +All: SEM_GI ;	

LEXICON ADESSIVE
          +Ade: SEM_GI ;	

LEXICON ABLATIVE
          +Abl: SEM_GI ;	

LEXICON TRANSLATIVE
          +Tra: SEM_GI ;	

LEXICON TERMINATIVE
          +Trm: SEM_GI ;	

LEXICON ESSIVE
          +Ess: SEM_GI ;	

LEXICON ABESSIVE
          +Abe: SEM_GI ;	

LEXICON COMITATIVE
          +Com: SEM_GI ;	

!! flag diacritics are used for remembering the case for compounding; 
!! not all the cases must be distinguished

LEXICON NOM_GI
  @P.Case.Nom@ GI ;

LEXICON GEN_GI
  @P.Case.Gen@ GI ;

LEXICON PAR_GI
  @P.Case.Par@ GI ;

LEXICON SEM_GI     ! semantic (aka oblique) case
  @P.Case.Sem@ GI ;

LEXICON PL_NOM_GI       ! in compounding, +Pl is relevant for +Nom only
  @P.Case.PlNom@ GI ;

! paradigmatic derivation, i.e. very regular

LEXICON M_DERIVATION
  @P.Der.A@ M_DERIVATION_SUF ;

LEXICON M_DERIVATION_SUF
          +Der/m+A+Comp:»m MAGUS ;           ! suurem, elanum,  etc

LEXICON IM_DERIVATION
   @P.Der.A@ IM_DERIVATION_SUF ;

LEXICON IM_DERIVATION_SUF
      +Der/im+A+Superl:m MAGUS ;         ! suurim, elanuim, õnnelikem etc; NB! notice that » is missing!
!       +Der/im+A+Superl:m LT_DERIVATION ; ! suurimalt, elanuimalt, õnnelikemalt

LEXICON NNA_DERIVATION
  @P.Der.N@ NNA_DERIVATION_SUF ;

LEXICON NNA_DERIVATION_SUF
            +Der/nna+N:»nna PERE ;             ! õpetajanna

LEXICON LANE_DERIVATION
  @P.Der.N@ LANE_DERIVATION_SUF ;

LEXICON LANE_DERIVATION_SUF
@U.Cap.Opt@@C.Cap@ LANE_DERIVATION_CAP ;         ! proper name must be downcased 

LEXICON LANE_DERIVATION_CAP
            +Der/lane+N:»lane OLULINE ;          ! helveetslane etc

LEXICON LIK_DERIVATION
  @P.Der.A@ LIK_DERIVATION_SUF ;

LEXICON LIK_DERIVATION_SUF
@U.Cap.Opt@@C.Cap@ LIK_DERIVATION_CAP ;         ! proper name must be downcased

LEXICON LIK_DERIVATION_CAP
            +Der/lik+A:»l´ik ELANIK ;           ! elajalik etc
            +Der/lik+A:»l´ikk US_DERIVATION ;  ! elajalikkus etc

LEXICON LINE_DERIVATION
  @P.Der.A@ LINE_DERIVATION_SUF ;

LEXICON LINE_DERIVATION_SUF
            +Der/line+A:»line OLULINE ;          ! põõsaline etc

LEXICON KOND_DERIVATION
  @P.Der.N@ KOND_DERIVATION_SUF ;

LEXICON KOND_DERIVATION_SUF
           +Der/kond+N:»k´onnD1%>%{pl.i%} PIIM ;  ! elajaskond etc
           +Der/kond+N:»konni TI_DERIVATION ;     ! elanikkonniti

LEXICON US_DERIVATION
  @P.Der.N@ US_DERIVATION_SUF ;

LEXICON US_DERIVATION_SUF
             +Der/us+N:»us OLULINE ;         ! elajalikkus, elavus, ...

LEXICON LT_DERIVATION
  @P.Der.Adv@ LT_DERIVATION_SUF ;

LEXICON LT_DERIVATION_SUF
            +Der/lt+Adv:»lt GI ;         ! derivation: suurelt, elanult, ...

LEXICON STI_DERIVATION
  @P.Der.Adv@ STI_DERIVATION_SUF ;

LEXICON STI_DERIVATION_SUF
          +Der/sti+Adv:»sti GI ;        ! valusasti, kuuldavasti

LEXICON INI_DERIVATION
  @P.Der.Adv@ INI_DERIVATION_SUF ;

LEXICON INI_DERIVATION_SUF
          +Der/ini+Adv:»ini GI ;        ! valusamini

LEXICON TI_DERIVATION
  @P.Der.Adv@ TI_DERIVATION_SUF ;

LEXICON TI_DERIVATION_SUF
           +Der/ti+Adv:»ti GI ;        ! maakonniti

LEXICON KE_DERIVATION
  @P.Der.N@ KE_DERIVATION_SUF ;    ! P.Der.N is not correct always

LEXICON KE_DERIVATION_SUF
               +Dim/ke:»ke PEENIKE ;         ! suureke, majake, õpetajannake, ...

LEXICON SHORT_NE_STEM
    @P.Case.Short@ SHORT_NE_STEM_SUF ;

LEXICON SHORT_NE_STEM_SUF
               +Der/minus:»%- # ;                ! vaatamis-, astraal-
!               +Der/minus:»# # ;                ! vaatamis#koht, astraal#tunne

