! inflectional types and analogue rules for nouns, adjectives and other declinables
! VVS stands for types from Ü. Viks "Väike vormisõnastik" for reference purposes

!---------------
! inflectional types
!---------------

!---------------
! not-so-exceptional inflectional types
!---------------

LEXICON AASTA    ! VVS 1 (productive) for vowel-final; stress on penultimate syllable words

           +Sg:%{no.apo%} NOMINATIVE ;    ! aasta
              : 2C ;    ! aasta etc

LEXICON REDEL    ! VVS 2 (productive) for consonant-ending, stress on penultimate syllable words
   
           +Sg:%{no.apo%} NOMINATIVE ;    ! redel, koppel
              :i 2C ;    ! redeli etc

LEXICON VIRSIK    ! VVS 2 (productive) for -ik, -ing; stress on penultimate syllable words

           +Sg:%{no.apo%} NOMINATIVE ;    ! virsik
              :u 2C ;    ! virsiku etc

LEXICON LOHAKAS    ! VVS 2 (productive) -kas, -jas etc ending words (incl derivations)

           +Sg:s NOMINATIVE ;    ! lohakas
              : 2C ;    ! lohaka etc

LEXICON TUNTUD    ! VVS 2 (productive) -d ending words (incl derivations)

           +Sg:d NOMINATIVE ;    ! tuntud
              : 2C ;    ! tuntu etc
 
LEXICON MAGUS    ! VVS 2 (unproductive) i.e. unstable (closed) type

           +Sg: NOMINATIVE ;    ! magus
              :A1 2C ;    ! magusa; A1 is necessary for pingne - pingsa

LEXICON HALE    ! VVS 2 (unproductive) i.e. unstable (closed) type

           +Sg: NOMINATIVE ;    ! hale, sajas
              :da 2C ;    ! haleda, sajanda

LEXICON HOOLAS    ! VVS 3 (unproductive) i.e. unstable (closed) type; strengthening gradation

           +Sg:%{W%} NOMINATIVE ;        ! hoolas
              :a 2C ;                    ! hoolsa etc
+Sg%+Use%/Rare:%{W%}%+t PARTITIVE ;      ! hoolast
%+Use%/NotNorm:%{W%}%+te N_PL_SUBPARA ;  ! hoolaste etc 

LEXICON HAARE    ! VVS 4, VVS 5 (unproductive) i.e. unstable (closed) type

              :%{W%} 2C_LEMMA2 ;         ! haare, haaret
              :me 2C_STRONGPL ;          ! haarme, habeme etc, haarmeid, haarmeil etc

LEXICON PINGE    ! VVS 6 (productive) i.e. stable 
                 ! (open to verbal derivations, e.g. kiune from kiunuma)
                 ! + (unproductive) i.e. unstable (closed) type; 

              :%{W%} 2C_LEMMA3 ;          ! pinge, pinget, pingete
              : 2C_STRONG ;               ! pinge etc, pingeid, pingeil etc

LEXICON KOTKAS    !  VVS 7 (unproductive) i.e. unstable (closed) type; 

              :s%{W%} 2C_LEMMA3 ;         ! kotkas, kotkast, kotkaste
              : 2C_STRONG ;               ! kotka etc, kotkaid, kotkail etc

LEXICON PIPAR    !  VVS 8 (unproductive) i.e. unstable (closed) type; = HOOLAS

              :%{W%} 2C_LEMMA3 ;          ! pipar, pipart, piparde
              :a 2C_STRONG ;
%+Use%/NotNorm:a%+te N_PL_SUBPARA ;       ! piprate etc 

LEXICON KANNEL    ! VVS 8 (unproductive) i.e.unstable (closed) type; = PIPAR, but stem vowel -e instead of -a

              :%{W%} 2C_LEMMA3 ;          ! kannel, kannelt, kannelde
              :e 2C_STRONG ;
%+Use%/NotNorm:e%+te N_PL_SUBPARA ;       ! kandlete, tütrete etc 

LEXICON SOOLANE    ! VVS 9 (productive) simplex words ending with -s (katus), VVS 10 affix -ne; 
                       
           +Sg: NOMINATIVE ;
              : NE_S_COMMON ;
           +Sg:E1%+sse ILLATIVE ;            ! soolasesse, katusesse
           +Pl:E1%+id PARTITIVE ;            ! soolaseid, katuseid
   %+Use%/Rare:E1%+i N_VOWEL_PL_SUBPARA ;    ! soolaseil, katuseil etc

LEXICON OLULINE    ! VVS 11, 12 (productive)
                   ! VVS 12: affixes -lane, -line, -mine, -tine, -ke(ne), -stikune (?)
                   ! VVS 11: derived words ending with -us, -is (?) (harjutus)
                  
           +Sg: NOMINATIVE ;
              : NE_S_COMMON ;
              : LINE_US_SPECIFIC ;

LEXICON KATKINE    ! VVS 9 & (11), 10 & (12) (productive)
                   ! VVS 10 & (12): affix -ne, but sg ill, pl part and vocal cases also like -line
                   ! VVS  9 & (11): simplex -s, but also like -us (gloobus) 
                 
              : SOOLANE ;
   %+Use%/Rare: LINE_US_NORM ;              ! like OLULINE: katkisse, katkisi
   %+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;    ! like OLULINE: katkisil etc; (ever used?)

LEXICON SUULINE    ! VVS (9) & 11, (10) & 12 (productive)
                   ! VVS (10) & 12: affix -line, but pl part and vowel cases also like -ne
                   ! VVS  (9) & 11: derived -us, but pl part and vowel cases (?) also like simplex -s (vaatlus) 

              : OLULINE ;
+Pl%+Use%/Rare:E1%+id PARTITIVE ;            ! like SOOLANE: suuliseid, vaatluseid
   %+Use%/Rare:E1%+i N_VOWEL_PL_SUBPARA ;    ! like SOOLANE: suuliseil, vaatluseil etc

LEXICON PEENIKE    ! VVS 12 (productive): affix -ke; almost = OLULINE, except sg nom

           +Sg: NOMINATIVE ;
+Sg%+Use%/Rare:ne NOMINATIVE ;
              :ne NE_S_COMMON ;
              :ne LINE_US_SPECIFIC ;

LEXICON PERE    ! VVS 16 (productive)

           +Sg:%{no.apo%} NOMINATIVE ;
              : 2V ;
           +Sg:%+t PARTITIVE ;    ! peret

LEXICON KAVA    ! VVS 17, 18 (productive) + (unproductive)
                ! VVS 17 (productive)
                ! VVS 18 (unproductive): words with quality gradation

           +Sg:%{no.apo%} NOMINATIVE ;
              : 2V ;
           +Sg: PARTITIVE ;       ! kava

LEXICON MUUSEUM    ! VVS 2 & 19 (productive)
                   ! forms may be based on 2 productive, i.e. stable types (aktiivne muuttüüp)
                   ! if prononunced as 2-syllable word (muus-jum), then like REDEL
                   ! if prononunced as 3-syllable word (muus-e-um), then like TAUD 

           +Sg:%{no.apo%} NOMINATIVE ;
              :i%{W%} N_SG_SUBPARA ; 
           +Sg:i PARTITIVE ;             ! muuseumi
+Sg%+Use%/Rare:i ILLATIVE ;              ! muuseumi  ! in TAUD, this is the default ill
           +Sg:i%{W%}%+sse ILLATIVE ;    ! muuseumisse ! in TAUD, this is the rare ill
              :i%+de N_PL_SUBPARA ;      ! muuseumide etc 
           +Pl:e PARTITIVE ;             ! muuseume

+Sg%+Use%/Rare:i%+t PARTITIVE ;          ! muuseumit
   %+Use%/Rare:i%+te N_PL_SUBPARA ;      ! muuseumite etc 
+Pl%+Use%/Rare:e%+id PARTITIVE ;         ! muuseumeid
   %+Use%/Rare:e%+i N_VOWEL_PL_SUBPARA ; ! muuseumeil etc

LEXICON PIIM    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) sepp, kaup, ...

           +Sg:%{no.apo%} NOMINATIVE ;
              :%{sg.a%} 1C ;

LEXICON KOON    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) lõpp, laug

           +Sg:%{no.apo%} NOMINATIVE ;
              :%{sg.u%} 1C ;

LEXICON EIT    ! VVS 22; (unproductive) i.e. unstable type (passiivne muuttüüp) kuusk, kärg

           +Sg:%{no.apo%} NOMINATIVE ;
              :%{sg.e%} 1C ;

LEXICON TAUD    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) siil, president, ...

           +Sg:%{no.apo%} NOMINATIVE ;
              :%+%{pl.e%}%{sg.i%} 1C ;    ! {pl.e} is here so it need not be in the stem lexicon

LEXICON FIAT    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) 
                ! almost == TAUD, but final stop weak grade does not change 

           +Sg:%{no.apo%} NOMINATIVE ;
              :i%{WL%} N_SG_SUBPARA ;      ! Fiati etc
           +Sg:i PARTITIVE ;               ! Fiati
           +Sg:i ILLATIVE ;                ! Fiati
 +Sg%+Use%/Hyp:i%{WL%}%+sse ILLATIVE ;     ! Fiatisse
              :%+%{pl.e%}%{sg.i%} DE_PL ;  ! Fiatide, Fiate, Fiatisid+Use/Rare 

LEXICON KABINET    ! VVS 22; (productive) i.e. stable type (aktiivne muuttüüp) 
                   ! almost == TAUD, but Sg Nom orthography with single final stop instead of 2

           +Sg:%{no.apo%} NOMINATIVE ;    ! kabinet
                                          ! sg nom has a single final stop instead of double, 
                                          ! so it has to be doubled to get the forms right
              :%.%+%{pl.e%}%{sg.i%} 1C ;

! VVS 24 is lexical variants under VVS 22 various subtypes

LEXICON ELANIK    ! VVS 25; (productive) i.e. stable type (aktiivne muuttüüp) 
                  ! almost == KABINET == TAUD

           +Sg:%{no.apo%} NOMINATIVE ;    ! elanik
                                          ! sg nom has a single final stop instead of double, 
                                          ! so it has to be doubled to get the forms right
              :%.%{sg.u%} 1C_SG ;         ! elaniku etc
              :%.e%{W%}%+ N_PL_SUBPARA ;    ! NB! elanike etc
   %+Use%/Rare:%.%{sg.u%}%+de N_PL_SUBPARA ;    ! elanikkude etc   
            +Pl:%.%+%{pl.e%}%{sg.u%}%+%{s%}%{i%}%{d%} PL_PAR_VARIANT ;    ! elanikke; elanikkusid+Use/Rare

LEXICON ARMEE    ! VVS 26 ; (productive) i.e. stable type (aktiivne muuttüüp)

              : 1V ;
           +Sg:%+sse ILLATIVE ;    ! armeesse

! ---------------
! sub-paradigms common to several inflectional types
! ---------------

LEXICON NE_S_COMMON ! VVS 9, 10, 11, 12 common forms for affix -ne, -line, -s, -tus etc 

              :E1 N_SG_SUBPARA ;                
           +Sg:E1%+t PARTITIVE ;         ! soolast, raudset, olulist
              :E1%+te N_PL_SUBPARA ;        

LEXICON LINE_US_SPECIFIC   ! -line, -us ending words
              : LINE_US_NORM ;
              : LINE_US_RARE ;

LEXICON LINE_US_RARE  ! part of line_us_specific

+Sg%+Use%/Rare:E1%+sse ILLATIVE ;          ! olulisesse, harjutusesse
   %+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;   ! olulisil, harjutusil etc

LEXICON LINE_US_NORM  ! part of line_us_specific

           +Sg:E1%+se ILLATIVE ;           ! olulisse, harjutusse (haplology)
           +Pl:%+I1 PARTITIVE ;            ! olulisi, harjutusi
      
! ------

LEXICON 1C    ! consonant ending, stress NOT on penultimate syllable

                           ! NOMINATIVE is without stem vowel
              : 1C_SG ;    ! singular forms + pl nom
              : DE_PL ;

LEXICON 1V    ! vowel ending, stress on the last syllable

           +Sg:%{no.apo%} NOMINATIVE ;    ! armee
              : N_SG_SUBPARA ;            ! armee etc
           +Sg:%+d PARTITIVE ;            ! armeed
              :%+de N_PL_SUBPARA ;        ! armeede etc  
           +Pl:%+%{s%}id PL_PAR_VARIANT ; ! armeesid; armeid...

LEXICON 2C    ! stress on penultimate syllable & word ends with a consonant; 
              ! or stressed penult. syllable extra heavy (e.g. -aania)  

                                          ! NOMINATIVE is without stem vowel
           +Sg:%+t PARTITIVE ;            ! redelit, virsikut
              : 2C_STRONGPL ;             ! redeleid, redeleil etc

! strengthening gradation paradigm consists of either:
! 2C_LEMMA2 + 2C_STRONGPL, or 
! 2C_LEMMA3 + 2C_STRONG

LEXICON 2C_LEMMA2    ! subparadigm where 2 stems = lemma  

           +Sg: NOMINATIVE ;              ! ase, haare
           +Sg:%+t PARTITIVE ;            ! aset, haaret

LEXICON 2C_LEMMA3    ! subparadigm where 3 stems = lemma  

           +Sg: NOMINATIVE ;              ! pinge
           +Sg:%+t PARTITIVE ;            ! pinget
              :%+te N_PL_SUBPARA ;        ! pingete etc

LEXICON 2C_STRONG    ! forms that would usually be in strong grade in strengthening gradation

              : N_SG_SUBPARA ;             ! redeli, kotka etc
           +Sg: SG_ILL_VARIANT ;           ! taskusse or tasku
           +Pl:%+id PARTITIVE ;            ! kotkaid
   %+Use%/Rare:%+i N_VOWEL_PL_SUBPARA ;    ! kotkail etc

LEXICON 2C_STRONGPL   ! forms that would sometimes be in strong grade in strengthening gradation

              : 2C_STRONG;                ! aseme, haarme etc
              :%+te N_PL_SUBPARA ;        ! asemete, haarmete

LEXICON 2V    ! stress on penultimate syllable, word ends with vowel 
              ! & stressed syllable NOT extra heavy (kava, sõda, pere)

              :%{WL%} N_SG_SUBPARA ;    ! kava, sõja etc
                                        ! PARTITIVE is +0 or +t
              : 2V_ILLATIVE ;
              : DE_PL ;

LEXICON 1C_SG    ! SG subpara of 1C

              :%{W%} N_SG_SUBPARA ;    ! koti etc
           +Sg: PARTITIVE ;            ! kotti
              : 1C_ILLATIVE ;          ! kotti, kotisse

LEXICON DE_PL    ! PL subpara of TAUD, KAVA, PERE

              :%+de N_PL_SUBPARA ;     ! kottide etc
                                       ! pl par stem vowel (u, i, 0) is often lexically determined 
                                       ! and usage-wise tagged
           +Pl:%+%{s%}%{i%}%{d%} PL_PAR_VARIANT ;   ! kingi, kingasid+Use/Rare vs. puudi+Use/Rare,
                                                    ! puudasid; kavu, kavasid; sõdu, sõdasid

! ---------------
! rules of analogy
! (cases grouped in various ways, for combining in different paradigms)
! ---------------

LEXICON N_SG_SUBPARA    ! sg gen + sg cases depending on it + pl nom; excl sg ill

           +Pl:%+d NOMINATIVE ;
           +Sg: REGULAR_GEN_ET_AL ;

LEXICON N_PL_SUBPARA    ! pl gen + pl cases depending on it 

           +Pl: STANDARD_GEN_ET_AL ;

LEXICON STANDARD_GEN_ET_AL    ! genitive and cases depending on it (either sg or pl)
                              ! incl. sse-illative 
              : GENITIVE ; 
              : STANDARD_SEM_CASES ;

LEXICON REGULAR_GEN_ET_AL    ! genitive and cases depending on it (either sg or pl)
                            ! NB! excl. illative 
              : GENITIVE ; 
              : REGULAR_SEM_CASES ;

LEXICON STANDARD_SEM_CASES    ! semantic cases, incl sse-illative

              :%+sse ILLATIVE ;
              : REGULAR_SEM_CASES ;

LEXICON REGULAR_SEM_CASES    ! semantic cases, excl illative

              : REGULAR_LOCATIVES ;
              :%+ks TRANSLATIVE ;
              : NINATAGA ;

LEXICON NINATAGA

              :%+ni TERMINATIVE ;	
              :%+na ESSIVE ;	
              :%+ta ABESSIVE ;	
              :%+ga COMITATIVE ;

	
LEXICON N_VOWEL_PL_SUBPARA    ! pl cases depending on vowel plural gen  
                              ! excl. ni-na-ta-ga 
           +Pl: LOCATIVES ;

LEXICON LOCATIVES            

              : STANDARD_LOCATIVES ;
              :%+ks TRANSLATIVE ;

LEXICON STANDARD_LOCATIVES            

              :%+sse ILLATIVE ;
              : REGULAR_LOCATIVES ;

LEXICON REGULAR_LOCATIVES            

              :%+s INESSIVE ;
              :%+st ELATIVE ;
              :%+le ALLATIVE ;	
              :%+l ADESSIVE ;	
              :%+lt ABLATIVE ;	

! -----------
! individual cases
! -----------

LEXICON 1C_ILLATIVE       ! sse-illative is very rare;
           +Sg: ILLATIVE ;              ! kotti
 +Sg%+Use%/Hyp:%{W%}%+sse ILLATIVE ;    ! kotisse

LEXICON 2V_ILLATIVE       ! sse-illative is the default; few words have the short form also
           +Sg:%{S%} SG_ILL_VARIANT ;     ! kavva
           +Sg:%{WL%} SG_ILL_VARIANT ;    ! kavasse 

! the preferred form for Pl Par and Sg Ill is indicated in the stem lexicon
! (not in the continuation lexicon)
! so these variants allow us to carry the info from stem lex to the final surface form

LEXICON PL_PAR_VARIANT    ! for TAUD, PIIM, KOON etc types

   %+Use%/Rare:%{rare%} PARTITIVE ;    ! less used pl par form for that word
              : PARTITIVE ;            ! default pl par form for that word

LEXICON SG_ILL_VARIANT    ! for KEEL etc types

   %+Use%/Rare:%+%{s%}%{s%}%{e%}%{rare%} ILLATIVE ;  ! less used sg ill form for that word
              :%+%{s%}%{s%}%{e%} ILLATIVE ;          ! default sg ill form for that word

LEXICON NOMINATIVE
          +Nom: GI ;

LEXICON GENITIVE
          +Gen: GI ;

LEXICON PARTITIVE
          +Par: GI ;	

LEXICON ILLATIVE
          +Ill: GI ;	

LEXICON INESSIVE
          +Ine: GI ;	

LEXICON ELATIVE
          +Ela: GI ;	

LEXICON ALLATIVE
          +All: GI ;	

LEXICON ADESSIVE
          +Ade: GI ;	

LEXICON ABLATIVE
          +Abl: GI ;	

LEXICON TRANSLATIVE
          +Tra: GI ;	

LEXICON TERMINATIVE
          +Trm: GI ;	

LEXICON ESSIVE
          +Ess: GI ;	

LEXICON ABESSIVE
          +Abe: GI ;	

LEXICON COMITATIVE
          +Com: GI ;	

