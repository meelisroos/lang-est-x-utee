
!! !!!Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes

! VVS 27
LEXICON ELAMA

+Impers:%+t IMPERS ; ! impersonal forms
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%+ IND_COND_PRS ; ! 

! VVS 27 julgema; like ELAMA, but there are parallel weak grade forms
LEXICON JULGEMA

+Impers:%+t IMPERS ; ! impersonal forms  julgeta...
+Impers%+Use%/Rare:%{W%}%+t IMPERS ; ! impersonal forms  juleta...
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes  julgenud...
+Pers+Prt%+Use%/Rare:%{W%}%+nu NU ;     !  NU-affixes julenud...
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%+ IND_COND_PRS ; ! 

! VVS 28
LEXICON LEPPIMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 

! VVS 28 & 27
! parallel forms in IMPERS and IND_COND_PRS; like SÜTTIMA, but non-grading forms are less used
LEXICON LEKKIMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Impers%+Use%/Rare:%+t IMPERS ; ! impersonal forms;  non-grading forms are less used
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 
+Pers+Prs%+Use%/Rare:%+ IND_COND_PRS ; ! non-grading forms are less used

! VVS 28 & 27
! parallel forms in IMPERS and IND_COND_PRS; like LEKKIMA, but weak grade forms are less used
LEXICON SÜTTIMA

+Impers%+Use%/Rare:%{W%}%+t IMPERS ; ! impersonal forms; weak grade forms are less used
+Impers:%+t IMPERS ; ! impersonal forms
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs%+Use%/Rare:%{W%}%+ IND_COND_PRS ; ! weak grade forms are less used
+Pers+Prs:%+ IND_COND_PRS ; ! 

! VVS 29
LEXICON HAKKAMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+t INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+k IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%{W%}%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%+ IND_COND_PRS ; ! 

! VVS 30
LEXICON SIPLEMA

+Impers:%{W%}%+d IMPERS ; ! impersonal forms
+Pers:e%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%{W%}%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:e%+s IND_PAST ; ! indicative past
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 30  ütlema, mõtlema; like SIPLEMA, but some parallel forms
LEXICON ÜTLEMA

+Impers:E2l%{W%}%+d IMPERS ; ! impersonal forms öeld..
+Impers%+Use%/Rare:tE2l%{W%}%+d IMPERS ; ! impersonal forms üteld...
+Pers:E2le%+ SUPINE_V ;  ! -ma etc, -v, -vat
:E2l%{W%}%+d INFINITIVE ;    ! infinitive, gerund (des-form) öeld...
%+Use%/Rare:tE2l%{W%}%+d INFINITIVE ;    ! infinitive, gerund (des-form) üteld...
+Pers+Prs+Imprt:E2l%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem  öelg...
+Pers+Prs+Imprt%+Use%/Rare:tE2l%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem ütelg...
+Pers+Prt:E2l%{W%}%+nu NU ;     !  NU-affixes öeln...
+Pers+Prt%+Use%/Rare:tE2l%{W%}%+nu NU ;     !  NU-affixes üteln...
+Pers+Prt+Ind:E2le%+s IND_PAST ; ! indicative past
+Pers+Prs:E2le%+ IND_COND_PRS ; ! 

! VVS 31
LEXICON RABELEMA  ! SIPLEMA + ELAMA 
                  ! assuming that rabel- is less used than rabele- ...

+Impers%+Use%/Rare:%+d IMPERS ; ! rabeldakse impersonal forms
+Impers:e%+t IMPERS ; ! rabeletakse impersonal forms
+Pers:e%+ SUPINE_V ;  ! rabele-ma etc, -v, -vat
%+Use%/Rare:%+d INFINITIVE ;    ! rabeldes infinitive, gerund (des-form)
:e%+d INFINITIVE ;    ! rabeledes infinitive, gerund (des-form)
+Pers+Prs+Imprt%+Use%/Rare:%+g IMPERATIVE ; ! rabel-gu/ku, -ge/ke, -gem/kem
+Pers+Prs+Imprt:e%+g IMPERATIVE ; ! rabele-gu/ku, -ge/ke, -gem/kem
+Pers+Prt%+Use%/Rare:%+nu NU ;     ! rabelnu NU-affixes
+Pers+Prt:e%+nu NU ;     ! rabelenu NU-affixes
+Pers+Prt+Ind:e%+s IND_PAST ; ! indicative past
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 32  seisma, maksma, peksma, lüpsma
LEXICON SEISMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1 seis+in
+Pers+Prt+Ind+Sg3+Aff:%+is GI ; ! seis+is
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 32  tõusma, naasma almost like SEISMA, but tõuseb (not tõusab)
LEXICON TÕUSMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1 tõus+in
+Pers+Prt+Ind+Sg3+Aff:%+is GI ; ! tõus+is
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 32 pesema
LEXICON PESEMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:e%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! pesi
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 32 kusema
LEXICON KUSEMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:e%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
%+Use%/NotNorm:e%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prs+Imprt%+Use%/NotNorm:e%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! pesi
+Pers+Prt+Ind%+Use%/NotNorm:e%+s IND_PAST ; ! indicative past
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 32  kaitsma, maitsma
LEXICON KAITSMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:e%+s IND_PAST ; ! indicative past
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 33  naerma, laulma, veenma, möönma, siirma 
LEXICON NAERMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 33  koolma, kuulma; almost like NAERMA, but kuuleb (not kuulab)
LEXICON KUULMA

+Impers:%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 34   hoidma
LEXICON HOIDMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 34   tundma
LEXICON TUNDMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 34   puskma, mõskma; almost like tundma
LEXICON PUSKMA

+Impers:%{W%}%+d IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+ IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 34   jooksma
LEXICON JOOKSMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms joostakse, ...
+Impers%+Use%/Rare:%+t IMPERS ; ! impersonal forms jookstakse,...
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+t INFINITIVE ;    ! infinitive, gerund (des-form)  joosta, joostes
%+Use%/Rare:%+t INFINITIVE ;    ! infinitive, gerund (des-form)  jooksta, jookstes
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prs+Imprt%+Use%/Rare:%{W%}%+g IMPERATIVE ; ! joos-gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1 jooks+in
+Pers+Prt+Ind+Sg3+Aff:%+is GI ; ! jooks+is
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 34   laskma
LEXICON LASKMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms lastakse, ...
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%{W%}%+t INFINITIVE ;    ! infinitive, gerund (des-form)  
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind%+Use%/Rare:I2%+s IND_PAST ; ! indicative past laskis, ...
+Pers+Prt+Ind:%{W%}%+ IND_PAST1 ; ! indicative past1 lasin, ...
+Pers+Prt+Ind+Sg3+Aff:%{W%}%+i GI ; ! lasi
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 34, 35   söötma, nutma
LEXICON SAATMA

+Impers:e%{W%}%+t IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 35   lõpma; almost like SAATMA
LEXICON LÕPMA

+Impers:e%{W%}%+t IMPERS ; ! impersonal forms
+Pers:E2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:I2%+s IND_PAST ; ! indicative past
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 35   katma kütma matma võtma jätma
! almost like SAATMA, but IMPERS are even weaker grade
LEXICON KATMA

+Impers:e%{W%}%+t IMPERS ; ! impersonal forms
+Pers:tE2%+ SUPINE_V ;  ! -ma etc, -v, -vat
:t%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:t%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:t%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:tI2%+s IND_PAST ; ! indicative past
+Pers+Prs:ta%{W%}%+ IND_COND_PRS ; ! 

! VVS 36   tulema
LEXICON TULEMA

+Impers:%+d IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:%.%+akse GI ;
+Impers+Prs+Ind+Aff%+Use%/NotNorm:%+dakse GI ;
+Pers:e%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! tuli
+Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! 

! VVS 36   olema
! (3rd grade symbols missing)
LEXICON OLEMA

+Impers:ol%+d IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:ol%.%+akse GI ;
+Impers+Prs+Ind+Aff%+Use%/NotNorm:ol%+dakse GI ;
+Pers:ole%+ SUPINE_V ;  ! -ma etc, -v, -vat
:ol%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:ol%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:ol%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:ol%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:ol%+i GI ; ! tuli

+Pers+Prs+Imprt+Sg2:ole%+ GI ;
+Pers+Prs+Ind+Neg:ole%+ GI ;
+Pers+Prs+Ind+Sg1+Aff:ole%+n GI ;
+Pers+Prs+Ind+Sg2+Aff:ole%+d GI ;
+Pers+Prs+Ind+Sg3+Aff:on GI ;
+Pers+Prs+Ind+Pl1+Aff:ole%+me GI ;
+Pers+Prs+Ind+Pl2+Aff:ole%+te GI ;
+Pers+Prs+Ind+Pl3+Aff:on GI ;
+Pers+Prs:ole%{W%}%+ CONDITIONAL ;

! + negative forms...
+Pers+Prs+Ind+Neg:pole%+ GI ;  ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%+ksin GI ;
+Pers+Prs+Cond+Sg2+Neg:pole%+ksid GI ;
+Pers+Prs+Cond+Neg:pole%+ks GI ;
+Pers+Prs+Cond+Pl1+Neg:pole%+ksime GI ;
+Pers+Prs+Cond+Pl2+Neg:pole%+ksite GI ;
+Pers+Prs+Cond+Pl3+Neg:pole%+ksid GI ;
+Pers+Prs+Quot+Neg:pole%+vat GI ;
+Pers+Prt+Ind+Neg:pol%+nud GI ;
+Pers+Prt+Cond+Sg1+Neg:pol%+nuksin GI ;
+Pers+Prt+Cond+Sg2+Neg:pol%+nuksid GI ;
+Pers+Prt+Cond+Neg:pol%+nuks GI ;
+Pers+Prt+Cond+Pl1+Neg:pol%+nuksime GI ;
+Pers+Prt+Cond+Pl2+Neg:pol%+nuksite GI ;
+Pers+Prt+Cond+Pl3+Neg:pol%+nuksid GI ;
+Pers+Prt+Quot+Neg:pol%+nuvat GI ;
+Impers+Prs+Ind+Neg:pol%+da GI ;  
+Impers+Prs+Cond+Neg:pol%+daks GI ;
+Impers+Prs+Quot+Neg:pol%+davat GI ;
+Impers+Prt+Ind+Neg:pol%+dud GI ;
+Impers+Prt+Cond+Neg:pol%+duks GI ;  ! almost non-attested form (by Google search) ... 


! VVS 36   minema
! (3rd grade symbols missing)
LEXICON MINEMA

+Impers:min%+d IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:min%.%+akse GI ;
+Impers+Prs+Ind+Aff%+Use%/NotNorm:min%+dakse GI ;
+Pers:mine%+ SUPINE_V ;  ! -ma etc, -v, -vat
:min%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:min%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:läi%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:läk%+s IND_PAST ; ! indicative past1

+Pers+Prs+Imprt+Sg2:mine%{W%}%+ GI ;
+Pers+Prs+Ind+Neg:lähe%{W%}%+ GI ;
+Pers+Prs+Ind+Sg1+Aff:lähe%{W%}%+n GI ;
+Pers+Prs+Ind+Sg2+Aff:lähe%{W%}%+d GI ;
+Pers+Prs+Ind+Sg3+Aff:lähe%{W%}%+b GI ;
+Pers+Prs+Ind+Pl1+Aff:lähe%{W%}%+me GI ;
+Pers+Prs+Ind+Pl1+Aff%+Use%/Rare:läh%{W%}%+me GI ; ! more colloquial...
+Pers+Prs+Ind+Pl2+Aff:lähe%{W%}%+te GI ;
+Pers+Prs+Ind+Pl3+Aff:lähe%{W%}%+vad GI ;
+Pers+Prs:lähe%{W%}%+ CONDITIONAL ;



! VVS 37   laama, keema, leema, naima, võima, näima
LEXICON KEEMA

+Impers:%{W%}%+d IMPERS ; ! impersonal forms
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 

! VVS 37   saama, jääma
LEXICON SAAMA

+Impers:%{W%}%+d IMPERS ; ! impersonal forms, except -akse
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! sai
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 

! VVS 38   viima, käima, pooma, müüma
LEXICON VIIMA

+Impers:%{W%}%+d IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:%{W%}%+akse GI ;
+Impers+Prs+Ind+Aff%+Use%/NotNorm:%{W%}%+dakse GI ;
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+s IND_PAST ; ! indicative past
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 

! VVS 38   jooma, looma, tooma, lööma, sööma
LEXICON TOOMA

+Impers:%{W%}%+d IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:%{W%}%+akse GI ;
+Impers+Prs+Ind+Aff%+Use%/NotNorm:%{W%}%+dakse GI ;
+Pers:%+ SUPINE_V ;  ! -ma etc, -v, -vat
:%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:%{W%}%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! jõi
+Pers+Prs:%{W%}%+ IND_COND_PRS ; ! 

! VVS 27 ajama; almost like VEDAMA
LEXICON AJAMA

+Impers:%+t IMPERS ; ! impersonal forms
+Pers:a%+ SUPINE_V ;  ! -ma etc, -v, -vat
:a%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:a%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:a%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:a%+s IND_PAST ; ! indicative past
+Pers+Prs:a%+ IND_COND_PRS ; ! 

! VVS 28 vedama, pügama, pidama
LEXICON VEDAMA

+Impers:e%{W%}%+t IMPERS ; ! impersonal forms
+Pers:a%+ SUPINE_V ;  ! -ma etc, -v, -vat
:a%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:a%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:a%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:a%+s IND_PAST ; ! indicative past
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 28 pidama-pidi
LEXICON PIDAMA

!+V+Impers:e%{W%}%+t IMPERS ; ! no impersonal forms
+Pers:a%+ SUPINE_V ;  ! -ma etc, -v, -vat
:a%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:a%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:a%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:%+i GI ; ! pidi
+Pers+Prs:a%{W%}%+ IND_COND_PRS ; ! 

! VVS 28 tegema, nägema
LEXICON TEGEMA

+Impers:h%+t IMPERS1 ; ! impersonal forms, except -akse
+Impers+Prs+Ind+Aff:h%+akse GI ;
+Pers:ge%+ SUPINE_V ;  ! -ma etc, -v, -vat
:h%+ INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:h%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:i%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:g%+ IND_PAST1 ; ! indicative past1
+Pers+Prt+Ind+Sg3+Aff:g%+i GI ; ! pidi
+Pers+Prs:e%+ IND_COND_PRS ; ! 

! VVS 28 käskima
LEXICON KÄSKIMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:i%+ SUPINE_V ;  ! -ma etc, -v, -vat
:i%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:i%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:i%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:i%+s IND_PAST ; ! indicative past
+Pers+Prs:i%{W%}%+ IND_COND_PRS ; ! 

! VVS 28 kiskuma ; like käskima, but u instead of i
LEXICON KISKUMA

+Impers:%{W%}%+t IMPERS ; ! impersonal forms
+Pers:u%+ SUPINE_V ;  ! -ma etc, -v, -vat
:u%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:u%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:u%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:u%+s IND_PAST ; ! indicative past
+Pers+Prs:u%{W%}%+ IND_COND_PRS ; ! 

! VVS 28 uskuma ; = kiskuma + parallel forms
LEXICON USKUMA

+Impers%+Use%/Rare:%{W%}%+t IMPERS ; ! impersonal forms
+Impers:u%{W%}%+t IMPERS ; ! impersonal forms
+Pers:u%+ SUPINE_V ;  ! -ma etc, -v, -vat
:u%+d INFINITIVE ;    ! infinitive, gerund (des-form)
+Pers+Prs+Imprt:u%+g IMPERATIVE ; ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prt:u%+nu NU ;     !  NU-affixes
+Pers+Prt+Ind:u%+s IND_PAST ; ! indicative past
+Pers+Prs:u%{W%}%+ IND_COND_PRS ; ! 

LEXICON IND_COND_PRS   ! indicative, conditional present + imp sg2

+Imprt+Sg2: GI ;
+Ind+Neg: GI ;
+Ind+Sg1+Aff:n GI ;
+Ind+Sg2+Aff:d GI ;
+Ind+Sg3+Aff:b GI ;
+Ind+Pl1+Aff:me GI ;
+Ind+Pl2+Aff:te GI ;
+Ind+Pl3+Aff:vad GI ;
: CONDITIONAL ;

LEXICON IND_PAST       ! indicative past
: IND_PAST1 ;
+Sg3+Aff: GI ;

LEXICON IND_PAST1       ! indicative past, except sg3

+Sg1+Aff:in GI ;
+Sg2+Aff:id GI ;
+Pl1+Aff:ime GI ;
+Pl2+Aff:ite GI ;
+Pl3+Aff:id GI ;

LEXICON NU      !  NU-affixes

: CONDITIONAL ;
! +Imprt:d GI ;      ! omitted for now because it would create homonymy
+Ind+Neg:d GI ;
+Prc:d GI ;
+Quot:vat GI ;

LEXICON IMPERATIVE      ! imperative

:u GI ;              ! elagu
+Pl1:em GI ;      ! elagem
+Pl2:e GI ;       ! elage

LEXICON SUPINE_V

+Sup:ma SUPINE ;
+Prs+Prc:v GI ;
+Prs+Quot:vat GI ;

LEXICON SUPINE

+Ill: GI ;     ! elama
+Ine:s GI ;    ! elamas
+Ela:st GI ;
+Tra:ks GI ;
+Abe:ta GI ;

LEXICON IMPERS  ! impersonal forms
: IMPERS1 ;
+Prs+Ind+Aff:akse GI ;

LEXICON IMPERS1  ! impersonal forms, except -[td]akse

+Prs+Cond:aks GI ;
+Prs+Imprt:agu GI ;
+Prs+Ind+Neg:a GI ;
+Prs+Prc:av GI ;
+Prs+Quot:avat GI ;
+Prt+Cond:uks GI ;
+Prt+Ind+Aff:i GI ;
+Prt+Ind+Neg:ud GI ;
+Prt+Prc:ud GI ;
+Sup:ama GI ;

LEXICON INFINITIVE     ! infinitive, gerund (des-form)

+Inf:a GI ;
+Ger:es GI ;

LEXICON CONDITIONAL    ! conditional present and past  

+Cond:ks GI ;
+Cond+Sg1+Aff:ksin GI ;
+Cond+Sg2+Aff:ksid GI ;
+Cond+Pl1+Aff:ksime GI ;
+Cond+Pl2+Aff:ksite GI ;
+Cond+Pl3+Aff:ksid GI ;


