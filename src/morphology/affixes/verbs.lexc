
!! !!!Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes
! VVS type numbers = type numbers of Õigekeesussõnastik 2013

LEXICON ELAMA    ! VVS 27

                      : IMPERS ;           ! elatud
                      : SUPINE_V ;         ! elama
                      : INFINITIVE ;       ! elada
                      : IMPERATIVE ;       ! elagu
                      : NU ;               ! elanud
                      :%+I7S7 IND_PAST ;   ! elas
                      : IND_COND_PRS ;     ! elan, elaksin

LEXICON JULGEMA   ! only julgema

                      : ELAMA ;    
           %+Use%/Rare:%{W%} IMPERS ;      ! impersonal forms  juleta...
           %+Use%/Rare:%{W%} NU ;          ! NU-affixes julenud...

LEXICON LEPPIMA   ! VVS 28

                      : LEPPIMA_STRONG ;
                      :%{W%} LEPPIMA_WEAK ;

LEXICON LEPPIMA_STRONG   ! strong grade sub-paradigm

                      : SUPINE_V ;        ! leppima
                      : INFINITIVE ;      ! leppida
                      : IMPERATIVE ;      ! leppigu
                      : NU ;              ! leppinud
                      :%+I7S7 IND_PAST ;  ! leppis

LEXICON LEPPIMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;          ! lepitud
                      : IND_COND_PRS ;    ! lepin, lepiksin

LEXICON LEKKIMA   ! VVS 28 & 27
                  ! like SÜTTIMA, but non-grading forms are less used

                      : LEPPIMA_STRONG ;         ! lekkima, lekib, lekitud, ...
                      :%{W%} LEPPIMA_WEAK ;      ! lekib, lekiks
           %+Use%/Rare: LEPPIMA_WEAK ;           ! lekkitud, lekkib, ...

LEXICON SÜTTIMA   ! VVS 28 & 27
                  ! like LEKKIMA, but weak grade forms are less used

                      : LEPPIMA_STRONG ;         ! süttima
                      : LEPPIMA_WEAK ;           ! süttib, süttiks
           %+Use%/Rare:%{W%} LEPPIMA_WEAK ;      ! sütitud, sütib, sütiks

LEXICON HAKKAMA    ! VVS 29 (hakkama), 30 (riidlema)

                      :%{W%} HAKKAMA_WEAK ;
                      : SUPINE_V ;            ! hakkama, riidlema
                      :%+I7S7 IND_PAST ;      ! hakkas, riidles
                      : IND_COND_PRS ;        ! hakkan, riidlen

LEXICON HAKKAMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;          ! hakatud, riieldud etc
                      : INFINITIVE ;      ! hakata, riielda
                      : IMPERATIVE ;      ! hakaku, riielgu
                      : NU ;              ! hakanud, riielnud

LEXICON ÜTLEMA    ! only ütlema, mõtlema
                  ! parallel forms: either t disappears in weak grade, or not

                      :le HAKKAMA ;             ! ütlema, ..., öeldud, öelda, öelgu, öelnud
           %+Use%/Rare:tle%{W%} HAKKAMA_WEAK ;  ! üteldud, ütelda, ütelgu, ütelnud

LEXICON RABELEMA    ! VVS 31; = HAKKAMA + ELAMA 
                    ! assuming that rabel- is less used than rabele- ...

                      : ELAMA ;                 ! rabelema; rabeletud, rabeleda, ...
           %+Use%/Rare:%{W%} HAKKAMA_WEAK ;     ! rabeldud, rabelda, ...

LEXICON KÄSKIMA     ! only käskima

                      :i LEPPIMA_STRONG ;       ! käskima etc
                      : IMPERS ;                ! kästud
                      :i%{W%} IND_COND_PRS ;    ! käsin etc

LEXICON KISKUMA     ! only kiskuma 

                      :u LEPPIMA_STRONG ;         ! kiskuma etc
                      : IMPERS ;                  ! kistud
           %+Use%/Rare:u%{W%} IMPERS ;            ! kisutud etc
                      :u%{W%} IND_COND_PRS ;      ! kisun etc

LEXICON USKUMA      ! only uskuma 

           %+Use%/Rare: IMPERS ;          ! ustud etc
                      :u LEPPIMA ;        ! uskuma, usutud etc

! ---------------
LEXICON NAERMA      ! VVS 32, 33  naerma, kuulma, seisma, tõusma etc (31 words)

                      : C_COMMON ;           ! naerda, seista, naergu, seisku, naernud, seisnud
                      : IMPERS ;             ! naerdud, seistud
                      : SUPINE_V ;           ! naerma, seisma
                      :%+I7S7 IND_PAST ;     ! naeris, seisis
                      :%{W%} IND_COND_PRS ;  ! naerab, seisab, kuuleb, tõuseb

LEXICON PESEMA    ! only pesema

                      : C_COMMON ;            ! pesta, pesku, pesnud
                      : IMPERS ;              ! pestud
                      :e SUPINE_V ;           ! pesema
                      :%+I7S7 IND_PAST ;      ! pesi
                      :%{W%} IND_COND_PRS ;   ! peseb

LEXICON KUSEMA    ! only kusema

                      : PESEMA ;    
        %+Use%/NotNorm:e INFINITIVE ;         ! kuseda
        %+Use%/NotNorm:e IMPERATIVE ;         ! kusegu
        %+Use%/NotNorm:e%+I7S7 IND_PAST ;     ! kuses

LEXICON KAITSMA    ! only  kaitsma, maitsma

                      : C_COMMON ;            ! kaitsta, kaitsku, kaitsnud
           %+Use%/Rare:e C_COMMON ;           ! kaitseda, kaitsegu, kaitsenud
                      : IMPERS ;              ! kaitstud
                      : SUPINE_V ;            ! kaitsma
           %+Use%/Rare:e SUPINE_V ;           ! kaitsema
                      :e%+I7S7 IND_PAST ;     ! kaitses
                      :e IND_COND_PRS ;       ! kaitseb

LEXICON SAATMA      ! VVS 34, 35   söötma, nutma; lõpma

                      : C_COMMON ;            ! sööta, nutta, söötke, nutke, söötnud, nutnud
                      :%{W%} IMPERS ;         ! söödetud, nutetud
                      : SUPINE_V ;            ! söötma, nutma
                      :%+I7S7 IND_PAST ;      ! söötis, nuttis
                      :%{W%} IND_COND_PRS ;   ! söödab, nutab; lõpeb

LEXICON JOOKSMA     ! only jooksma

                      :%{W%} IMPERS ;         ! impersonal forms joostakse, ... 
           %+Use%/Rare: IMPERS ;              ! impersonal forms jookstakse,...
                      : SUPINE_V ;            ! jooksma
                      :%{W%} INFINITIVE ;     ! joosta, joostes 
           %+Use%/Rare: INFINITIVE ;          ! jooksta, jookstes
                      : IMPERATIVE ;          ! jooksku
           %+Use%/Rare:%{W%} IMPERATIVE ;     ! joosku
                      : NU ;                  ! jooksnud
                      :%+I7S7 IND_PAST ;      ! jooksis, jooksin
                      :e IND_COND_PRS ;       ! jookseb

LEXICON LASKMA    ! only laskma

                      : C_COMMON ;            ! lasta, lasku, lasknud
                      : IMPERS ;              ! impersonal forms lastakse, ...
                      : SUPINE_V ;            ! laskma
           %+Use%/Rare:%+I7S7 IND_PAST ;      ! indicative past laskis, ...
                      :%{W%}%+I7S7 IND_PAST ; ! indicative past lasi, ...
                      :e%{W%} IND_COND_PRS ;  ! laseb

LEXICON KATMA      ! VVS 35   katma kütma matma võtma jätma
                   ! almost like SAATMA, but IMPERS are even weaker grade

                      :t C_COMMON ;          ! kütta
                      :%{W%} IMPERS ;        ! köetud
                      :t SUPINE_V ;          ! kütma
                      :t%+I7S7 IND_PAST ;    ! küttis
                      :a IND_COND_PRS ;      ! kütab

LEXICON C_COMMON  ! common to almost all words of types 32-35 (supine stem ends with C)

                      : INFINITIVE ;        ! naerda, seista, sööta, nutta, kütta
                      : IMPERATIVE ;        ! naergu, seisku, söötku, nutku, kütku
                      : NU ;                ! kütnud


! ---------------
LEXICON TULEMA     ! only tulema, panema, surema

                      :%. A_AKSE ;           ! surra, surrakse, surdud
                      :e SUPINE_V ;          ! surema
                      :%{W%} IMPERATIVE ;    ! surgu
                      :%{W%} NU ;            ! surnud
                      :%+I7 IND_PAST ;       ! suri, surin, ...
                      :e%{W%} IND_COND_PRS ; ! sureb

LEXICON OLEMA    ! only olema 

                      :ol%. A_AKSE ;          ! olla, ollakse, oldud
                      :ole SUPINE_V ;         ! olema
                      :ol%{W%} IMPERATIVE ;   ! olgu
                      :ol%{W%} NU ;           ! olnud
                      :ol%+I7 IND_PAST ;      ! oli, olin, ...

   +Pers+Prs+Imprt+Sg2:ole%+ GI ;
     +Pers+Prs+Ind+Neg:ole%+ GI ;
         +Pers+Prs+Ind:ole SG1 ;
         +Pers+Prs+Ind:ole SG2 ;
 +Pers+Prs+Ind+Sg3+Aff:on GI ;
         +Pers+Prs+Ind:ole PL1 ;
         +Pers+Prs+Ind:ole PL2 ;
 +Pers+Prs+Ind+Pl3+Aff:on GI ;
             +Pers+Prs:ole%{W%}%+ CONDITIONAL ;

! + negative forms...
     +Pers+Prs+Ind+Neg:pole%+ GI ;        ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%+ksin GI ;
+Pers+Prs+Cond+Sg2+Neg:pole%+ksid GI ;
    +Pers+Prs+Cond+Neg:pole%+ks GI ;
+Pers+Prs+Cond+Pl1+Neg:pole%+ksime GI ;
+Pers+Prs+Cond+Pl2+Neg:pole%+ksite GI ;
+Pers+Prs+Cond+Pl3+Neg:pole%+ksid GI ;
    +Pers+Prs+Quot+Neg:pole%+vat GI ;
     +Pers+Prt+Ind+Neg:pol%+nud GI ;
+Pers+Prt+Cond+Sg1+Neg:pol%+nuksin GI ;
+Pers+Prt+Cond+Sg2+Neg:pol%+nuksid GI ;
    +Pers+Prt+Cond+Neg:pol%+nuks GI ;
+Pers+Prt+Cond+Pl1+Neg:pol%+nuksime GI ;
+Pers+Prt+Cond+Pl2+Neg:pol%+nuksite GI ;
+Pers+Prt+Cond+Pl3+Neg:pol%+nuksid GI ;
    +Pers+Prt+Quot+Neg:pol%+nuvat GI ;
   +Impers+Prs+Ind+Neg:pol%+da GI ;  
  +Impers+Prs+Cond+Neg:pol%+daks GI ;
  +Impers+Prs+Quot+Neg:pol%+davat GI ;
   +Impers+Prt+Ind+Neg:pol%+dud GI ;
  +Impers+Prt+Cond+Neg:pol%+duks GI ;     ! almost non-attested form (by Google search) ... 

LEXICON MINEMA    ! only minema 

                      :min%. A_AKSE ;            ! minna, minnakse, mindud
                      :mine SUPINE_V ;           ! minema
                      :min%{W%} IMPERATIVE ;   ! mingu
                      :läi%{W%} NU ;             ! läinud
                      :läk%+s IND_PAST ;         ! läks, läksin etc

   +Pers+Prs+Imprt+Sg2:mine%{W%}%+ GI ;          ! mine
             +Pers+Prs:lähe%{W%} IND_PRS ;       ! lähen, läheb, ...
+Pers+Prs+Ind+Pl1+Aff%+Use%/Rare:läh%{W%}%+me GI ;  ! lähme more colloquial...
             +Pers+Prs:lähe%{W%}%+ CONDITIONAL ;  ! läheks, ...

! ---------------
! 17 exceptional words of types 37, 38, all ending in VV

LEXICON KEEMA   ! VVS 37   laama, keema, leema, naima, võima, näima

                      : VV_COMMON ;
                      : DA_DAKSE ;          ! keeda, keedakse, keedud
                      :%+I7S7 IND_PAST ;    ! kees

LEXICON SAAMA !!= @CODE@ is a lexicon for saama-type words. ! VVS 37   saama, jääma

                      : VV_COMMON ;
                      : DA_DAKSE ;          ! saada, saadakse, saadud
                      :%+I7 IND_PAST ;      ! sai

LEXICON VIIMA     ! VVS 38   viima, käima, pooma, müüma

                      : VV_COMMON ;
                      : A_AKSE ;             ! viia, viiakse, viidud
                      :%+I7S7 IND_PAST ;     ! viis

LEXICON TOOMA     ! VVS 38   jooma, looma, tooma, lööma, sööma

                      : VV_COMMON ;
                      : A_AKSE ;             ! juua, juuakse, joodud
                      :%+I7 IND_PAST ;       ! jõi
 
LEXICON VV_COMMON  ! common to all 17 words of types 37, 38 (supine stem ends with VV)

                      : SUPINE_V ;            ! keema, saama etc
                      :%{W%} IMPERATIVE ;     ! keegu, saagu etc
                      :%{W%} NU ;             ! keenud, saanud etc
                      :%{W%} IND_COND_PRS ;   ! keeb, saab etc

LEXICON A_AKSE     ! if infinitive affix is -a, then ...

                      :%+d IMPERS_DUD ;        ! surdud, söödud
                      :%{W%}%+ IMPERS_AKSE ;   ! surrakse, süüakse
        %+Use%/NotNorm:%+d IMPERS_AKSE ;       ! surdakse, söödakse
                      :%+ A_INFINITIVE ;       ! surra, süüa

LEXICON DA_DAKSE     ! if infinitive affix is -da, then ...

                      :%+d IMPERS_DUD ;     ! keedud
                      :%+d IMPERS_AKSE ;    ! keedakse
                      : INFINITIVE ;        ! keeda


! ---------------

LEXICON AJAMA      ! only ajama; almost like VEDAMA

                      :%{W%} IMPERS ;        ! aetud
                      :a SUPINE_V ;          ! ajama
                      :a INFINITIVE ;        ! ajada
                      :a IMPERATIVE ;        ! ajagu
                      :a NU ;                ! ajanud
                      :a%+I7S7 IND_PAST ;    ! ajas
                      :a IND_COND_PRS ;      ! ajab

LEXICON VEDAMA     ! VVS 28 vedama, pügama, pidama

                      :e%{W%} IMPERS ;       ! veetud
                      :a SUPINE_V ;          ! vedama
                      :a INFINITIVE ;        ! vedada
                      :a IMPERATIVE ;        ! vedagu
                      :a NU ;                ! vedanud
                      :a%+I7S7 IND_PAST ;    ! vedas
                      :a%{W%} IND_COND_PRS ; ! veab

LEXICON PIDAMA     ! only pidama-pidi

                      ! no impersonal forms
                      :a SUPINE_V ;          ! pidama
                      :a INFINITIVE ;        ! pidada
                      :a IMPERATIVE ;        ! pidagu
                      :a NU ;                ! pidanud
                      :%+I7 IND_PAST ;       ! pidi
                      :a%{W%} IND_COND_PRS ; ! peab

LEXICON TEGEMA     ! only tegema, nägema

                      :h A_AKSE ;            ! teha, tehakse, tehtud
                      :ge SUPINE_V ;         ! tegema
                      :h IMPERATIVE ;        ! tehku
                      :i NU ;                ! teinud
                      :g%+I7 IND_PAST ;      ! tegi
                      :e%{W%} IND_COND_PRS ; ! teeb

LEXICON IND_COND_PRS    ! indicative, conditional present + imp sg2

   +Pers+Prs+Imprt+Sg2:%+ GI ;
             +Pers+Prs:%+ IND_PRS ;
             +Pers+Prs:%+ CONDITIONAL ;

LEXICON IND_PRS     ! indicative present 
              +Ind+Neg: GI ;
                  +Ind: SG1 ;
                  +Ind: SG2 ;
                  +Ind: SG3B ;
                  +Ind: PL1 ;
                  +Ind: PL2 ;
                  +Ind: PL3VAD ;
            
LEXICON IND_PAST    ! indicative past
         +Pers+Prt+Ind:i MARKED_PERSON ;
         +Pers+Prt+Ind: SG3 ;

LEXICON NU    !  NU-affixes

             +Pers+Prt:%+nu CONDITIONAL ; ! conditional past
      !+Pers+Prt+Imprt:%+nud GI ;         ! omitted for now because it would create homonymy
     +Pers+Prt+Ind+Neg:%+nud GI ;
         +Pers+Prt+Prc:%+nud GI ;
        +Pers+Prt+Quot:%+nuvat GI ;
                      : NU_DERIVATION ;

LEXICON IMPERATIVE    ! imperative

       +Pers+Prs+Imprt:%+G7u GI ;         ! elagu
   +Pers+Prs+Imprt+Pl1:%+G7em GI ;        ! elagem
   +Pers+Prs+Imprt+Pl2:%+G7e GI ;         ! elage

LEXICON SUPINE_V

             +Pers+Sup:%+ma SUPINE ;
         +Pers+Prs+Prc:%+v GI ;
        +Pers+Prs+Quot:%+vat GI ;
                      : MINE_DERIVATION ;    ! elamine
                      : JA_DERIVATION ;      ! elaja, õpetaja, õpetajanna, õpetajake, õpetajalik, ...
                      : V_DERIVATION ;       ! õpetav, õpetavalt, õpetavus, õpetavam, ...
                      : MATU_DERIVATION ;    ! segamatu, segamatult, segamatus, segamatum, ...
                      : MATA_DERIVATION ;     ! segamata
                      : US_DERIVATION ;     ! õpetus

LEXICON SUPINE

                      : ILLATIVE ;        ! elama
                      :s INESSIVE ;       ! elamas
                      :st ELATIVE ;
                      :ks TRANSLATIVE ;
                      :ta ABESSIVE ;

LEXICON IMPERS    ! impersonal forms for most paradigms

                      :%+T7 IMPERS_DUD ;
                      :%+T7 IMPERS_AKSE ;

LEXICON IMPERS_DUD    ! impersonal forms -[td]ud

      +Impers+Prs+Cond:aks GI ;
     +Impers+Prs+Imprt:agu GI ;
   +Impers+Prs+Ind+Neg:a GI ;
       +Impers+Prs+Prc:av GI ;
      +Impers+Prs+Quot:avat GI ;
      +Impers+Prt+Cond:uks GI ;
   +Impers+Prt+Ind+Aff:i GI ;
   +Impers+Prt+Ind+Neg:ud GI ;
       +Impers+Prt+Prc:ud GI ;
           +Impers+Sup:ama GI ;
                      : TAV_DERIVATION ;       ! elatav, elatavalt, elatavam, elatavaim, ... 
                      : TAMATU_DERIVATION ;       ! elatav, elatavalt, elatavam, elatavaim, ... 
                      : TU_DERIVATION ;       ! elatu, elatult, elatum, elatuim 

LEXICON IMPERS_AKSE    ! impersonal form -[td0]akse

   +Impers+Prs+Ind+Aff:akse GI ;

LEXICON INFINITIVE    ! infinitive, gerund (des-form)

                      :%+D7 A_INFINITIVE ;

LEXICON A_INFINITIVE   ! common part of an infinitive, gerund (des-form);
                       ! also alone in a subparadigm of a few old words

                  +Inf:a GI ;
                  +Ger:es GI ;

LEXICON CONDITIONAL    ! conditional present and past  

                 +Cond:ks GI ;
                 +Cond:ksi MARKED_PERSON ;

LEXICON MARKED_PERSON     ! sg3 ~ underspecified is missing from here 
                      : SG1 ;
                      : SG2 ;
                      : PL1 ;
                      : PL2 ;
                      : PL3 ;

LEXICON SG1
              +Sg1+Aff:n GI ;

LEXICON SG2
              +Sg2+Aff:d GI ;

LEXICON SG3
              +Sg3+Aff: GI ;

LEXICON SG3B
              +Sg3+Aff:b GI ;

LEXICON PL1
              +Pl1+Aff:me GI ;

LEXICON PL2
              +Pl2+Aff:te GI ;

LEXICON PL3
              +Pl3+Aff:d GI ;

LEXICON PL3VAD
              +Pl3+Aff:vad GI ;

! paradigmatic derivation, i.e. very regular

LEXICON NU_DERIVATION

             +Der/nu+N:%+nu AASTA ;             ! derivation: elanu etc
             +Der/nud+A:%+nu TUNTUD ;             ! derivation: elanud etc
             +Der/nud+A:%+nud GI ;             ! derivation: noninflecting A: elanud 
!             +Der/nu+N:%+nu LT_DERIVATION ;     ! derivation: elanult
!             +Der/nu+N:%+nu M_DERIVATIONS ;     ! derivation: elanum,  etc
!             +Der/nu+N:%+nu%+i IM_DERIVATIONS ; ! derivation: elanuim, etc

LEXICON TU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon

             +Der/tu+A:u AASTA ;              ! derivation: elatu etc
             +Der/tud+A:%+tud GI ;             ! derivation: noninflecting A: elatud 
!             +Der/tu+A:u LT_DERIVATION ;      ! derivation: elatult
!             +Der/tu+A:u M_DERIVATIONS ;      ! derivation: elatum,  etc
!             +Der/tu+A:u US_DERIVATIONS ;      ! derivation: elatus,  etc
!             +Der/tu+A:u%+i IM_DERIVATIONS ;  ! derivation: elatuim, etc

LEXICON MATA_DERIVATION
           +Der/mata+A:%+mata GI ;            ! elamata 

LEXICON MATU_DERIVATION
           +Der/matu+A:%+matu AASTA ;             ! segamatu etc
!          +Der/matu+A:%+matu KE_DERIVATION ;     ! segamatuke etc
!           +Der/matu+A:%+matu LT_DERIVATION ;     ! segamatult
!           +Der/matu+A:%+matu US_DERIVATIONS ;     ! segamatus etc
!           +Der/matu+A:%+matu M_DERIVATIONS ;     ! segamatum
!           +Der/matu+A:%+matu%+i IM_DERIVATIONS ; ! segamatuim

LEXICON TAMATU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon
           +Der/tamatu+A:amatu AASTA ;             ! segatamatu etc
!          +Der/tamatu+A:amatu KE_DERIVATION ;     ! segatamatuke etc
!           +Der/tamatu+A:amatu LT_DERIVATION ;     ! segatamatult
!           +Der/tamatu+A:amatu US_DERIVATIONS ;     ! segatamatus etc
!           +Der/tamatu+A:amatu M_DERIVATIONS ;     ! segatamatum
!           +Der/tamatu+A:amatu%+i IM_DERIVATIONS ; ! segatamatuim

LEXICON V_DERIVATION
              +Der/v+A:%+v MAGUS ;              ! elav, elava etc
!              +Der/v+A:%+va KE_DERIVATION ;     ! elavake etc
!              +Der/v+A:%+va LT_DERIVATION ;     ! elavalt
!              +Der/v+A:%+va US_DERIVATIONS ;     ! elavus etc
!              +Der/v+A:%+va M_DERIVATIONS ;     ! elavam
!              +Der/v+A:%+va%+i IM_DERIVATIONS ; ! elavaim

LEXICON TAV_DERIVATION   !    NB! the morpheme border + d/t is assigned before this lexicon
            +Der/tav+A:av MAGUS ;              ! elatav, elatava etc
!            +Der/tav+A:ava KE_DERIVATION ;     ! elatavake etc
!            +Der/tav+A:ava LT_DERIVATION ;     ! elatavalt
!            +Der/tav+A:ava STI_DERIVATION ;    ! kuuldavasti
!            +Der/tav+A:ava US_DERIVATIONS ;     ! elatavus etc
!            +Der/tav+A:ava M_DERIVATIONS ;     ! elatavam
!            +Der/tav+A:ava%+i IM_DERIVATIONS ; ! elatavaim

LEXICON MINE_DERIVATION
           +Der/mine+N:%+mine OLULINE ;         ! elamine
!           +Der/mine+N:%+mise KE_DERIVATION ;   ! elamiseke

LEXICON JA_DERIVATION
             +Der/ja+N:%+ja AASTA ;             ! elaja, elajat etc
!             +Der/ja+N:%+ja KE_DERIVATION ;     ! elajake, elajakese etc
!             +Der/ja+N:%+ja NNA_DERIVATIONS ;   ! õpetajanna, õpetajannake, õpetajannalik, etc
!             +Der/ja+N:%+ja LIK_DERIVATIONS ;   ! õpetajannalik, õpetajannalikkus, etc
             +Der/ja+N:%+jas KOND_DERIVATION ; ! õpetajaskond, õpetajaskondlik, etc



