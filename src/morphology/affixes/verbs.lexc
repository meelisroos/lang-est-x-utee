
!! !!!Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes

LEXICON ELAMA    ! VVS 27

                      : IMPERS ;        ! impersonal forms
                 +Pers:%+ SUPINE_V ;       ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;           !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;      ! indicative past
             +Pers+Prs:%+ IND_COND_PRS ;   ! 

LEXICON JULGEMA   ! VVS 27 julgema; like ELAMA, but there are parallel weak grade forms

                      : ELAMA ;    
           %+Use%/Rare:%{W%} IMPERS ;    ! impersonal forms  juleta...
  +Pers+Prt%+Use%/Rare:%{W%}%+nu NU ;       !  NU-affixes julenud...

LEXICON LEPPIMA   ! VVS 28

                      : LEPPIMA_STRONG ;
                      :%{W%} IMPERS ;         ! impersonal forms
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 

LEXICON LEPPIMA_STRONG   ! strong grade sub-paradigm

                 +Pers:%+ SUPINE_V ;       ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;           !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;      ! indicative past


LEXICON LEKKIMA   ! VVS 28 & 27
                  ! like SÜTTIMA, but non-grading forms are less used

                      :%{W%} IMPERS ;    ! impersonal forms
           %+Use%/Rare: IMPERS ;         ! impersonal forms;  non-grading forms are less used
                      : LEPPIMA_STRONG ;
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 
  +Pers+Prs%+Use%/Rare:%+ IND_COND_PRS ;         ! non-grading forms are less used

LEXICON SÜTTIMA   ! VVS 28 & 27
                  ! like LEKKIMA, but weak grade forms are less used

           %+Use%/Rare:%{W%} IMPERS ;    ! impersonal forms; weak grade forms are less used
                      : IMPERS ;         ! impersonal forms
                      : LEPPIMA_STRONG ;
  +Pers+Prs%+Use%/Rare:%{W%}%+ IND_COND_PRS ;    ! weak grade forms are less used
             +Pers+Prs:%+ IND_COND_PRS ;         ! 

LEXICON HAKKAMA    ! VVS 29

                      :%{W%} IMPERS ;        ! impersonal forms
                 +Pers:%+ SUPINE_V ;            ! -ma etc, -v, -vat
                      :%{W%}%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%{W%}%+nu NU ;           !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;           ! indicative past
             +Pers+Prs:%+ IND_COND_PRS ;        ! 

! LEXICON SIPLEMA    ! VVS 30 = HAKKAMA VVS 29


LEXICON ÜTLEMA    ! VVS 30  only ütlema, mõtlema; like SIPLEMA, but some parallel forms
                  ! either t disappears in weak grade, or not

                      :le%{W%} IMPERS ;    ! impersonal forms öeld..
           %+Use%/Rare:tle%{W%} IMPERS ;    ! impersonal forms üteld...
                 +Pers:le%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :le%{W%}%+D7 INFINITIVE ;    ! infinitive, gerund (des-form) öeld...
           %+Use%/Rare:tle%{W%}%+D7 INFINITIVE ;    ! infinitive, gerund (des-form) üteld...
       +Pers+Prs+Imprt:le%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem  öelg...
+Pers+Prs+Imprt%+Use%/Rare:tle%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem ütelg...
             +Pers+Prt:le%{W%}%+nu NU ;    !  NU-affixes öeln...
  +Pers+Prt%+Use%/Rare:tle%{W%}%+nu NU ;    !  NU-affixes üteln...
         +Pers+Prt+Ind:le%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:le%+ IND_COND_PRS ;    ! 

LEXICON RABELEMA    ! VVS 31; = SIPLEMA + ELAMA 
                    ! assuming that rabel- is less used than rabele- ...

                       : ELAMA ;
            %+Use%/Rare:%{W%} IMPERS ;    ! rabeldakse impersonal forms
            %+Use%/Rare:%{W%}%+D7 INFINITIVE ;    ! rabeldes infinitive, gerund (des-form)
+Pers+Prs+Imprt%+Use%/Rare:%{W%}%+G7 IMPERATIVE ;    ! rabel-gu/ku, -ge/ke, -gem/kem
   +Pers+Prt%+Use%/Rare:%{W%}%+nu NU ;    ! rabelnu NU-affixes

! VVS 32, 33  naerma, seisma etc 
LEXICON NAERMA

                      : IMPERS ;    ! impersonal forms
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! 

! VVS 32, 33  kuulma, tõusma etc; almost like NAERMA, but kuuleb (not kuulab)
LEXICON KUULMA

                      : IMPERS ;    ! impersonal forms
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! 

LEXICON PESEMA    ! VVS 32 pesema

                      : IMPERS ;    ! impersonal forms
                 +Pers:e%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;           ! indicative past
             +Pers+Prs:e%+ IND_COND_PRS ;    ! 

LEXICON KUSEMA    ! VVS 32 kusema

                      : IMPERS ;    ! impersonal forms
                 +Pers:e%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
        %+Use%/NotNorm:e%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prs+Imprt%+Use%/NotNorm:e%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;           ! indicative past
+Pers+Prt+Ind%+Use%/NotNorm:e%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%+ IND_COND_PRS ;    ! 

LEXICON KAITSMA    ! VVS 32  kaitsma, maitsma

                      : IMPERS ;    ! impersonal forms
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:e%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%+ IND_COND_PRS ;    ! 

! VVS 34   jooksma
LEXICON JOOKSMA

                      :%{W%} IMPERS ;    ! impersonal forms joostakse, ... 
           %+Use%/Rare: IMPERS ;    ! impersonal forms jookstakse,...
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%{W%}%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)  joosta, joostes 
           %+Use%/Rare:%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)  jooksta, jookstes
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
+Pers+Prs+Imprt%+Use%/Rare:%{W%}%+G7 IMPERATIVE ;    ! joos-gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%+ IND_COND_PRS ;    ! 

! VVS 34   laskma
LEXICON LASKMA

                      : IMPERS ;    ! impersonal forms lastakse, ...
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)  
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
+Pers+Prt+Ind%+Use%/Rare:%+I7S7 IND_PAST ;    ! indicative past laskis, ...
         +Pers+Prt+Ind:%{W%}%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! 

! VVS 34, 35   söötma, nutma
LEXICON SAATMA

                      :%{W%} IMPERS ;    
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! 

! VVS 35   lõpma; almost like SAATMA
LEXICON LÕPMA

                      :e%{W%} IMPERS ;    
!                      :%{W%} IMPERS ;    
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! 

! VVS 35   katma kütma matma võtma jätma
! almost like SAATMA, but IMPERS are even weaker grade
LEXICON KATMA

                      :%{W%} IMPERS ;    
                 +Pers:t%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :t%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:t%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:t%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:t%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:a%+ IND_COND_PRS ;    ! 

! VVS 36   tulema, panema, surema
LEXICON TULEMA

               +Impers:%.%+d IMPERS_DUD ;    
               +Impers:%.%{W%}%+ IMPERS_AKSE ;
 +Impers%+Use%/NotNorm:%.%+d IMPERS_AKSE ;
                 +Pers:e%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7 IND_PAST ;    ! indicative past
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! 

! VVS 36   olema
! (3rd grade symbols missing)
LEXICON OLEMA

               +Impers:ol%.%+d IMPERS_DUD ;    ! impersonal forms, except -akse
               +Impers:ol%.%{W%}%+ IMPERS_AKSE ;
 +Impers%+Use%/NotNorm:ol%.%+d IMPERS_AKSE ;
                 +Pers:ole%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :ol%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:ol%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:ol%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:ol%+I7 IND_PAST ;    ! indicative past

   +Pers+Prs+Imprt+Sg2:ole%+ GI ;
     +Pers+Prs+Ind+Neg:ole%+ GI ;
 +Pers+Prs+Ind+Sg1+Aff:ole%+n GI ;
 +Pers+Prs+Ind+Sg2+Aff:ole%+d GI ;
 +Pers+Prs+Ind+Sg3+Aff:on GI ;
 +Pers+Prs+Ind+Pl1+Aff:ole%+me GI ;
 +Pers+Prs+Ind+Pl2+Aff:ole%+te GI ;
 +Pers+Prs+Ind+Pl3+Aff:on GI ;
             +Pers+Prs:ole%{W%}%+ CONDITIONAL ;

! + negative forms...
     +Pers+Prs+Ind+Neg:pole%+ GI ;    ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%+ksin GI ;
+Pers+Prs+Cond+Sg2+Neg:pole%+ksid GI ;
    +Pers+Prs+Cond+Neg:pole%+ks GI ;
+Pers+Prs+Cond+Pl1+Neg:pole%+ksime GI ;
+Pers+Prs+Cond+Pl2+Neg:pole%+ksite GI ;
+Pers+Prs+Cond+Pl3+Neg:pole%+ksid GI ;
    +Pers+Prs+Quot+Neg:pole%+vat GI ;
     +Pers+Prt+Ind+Neg:pol%+nud GI ;
+Pers+Prt+Cond+Sg1+Neg:pol%+nuksin GI ;
+Pers+Prt+Cond+Sg2+Neg:pol%+nuksid GI ;
    +Pers+Prt+Cond+Neg:pol%+nuks GI ;
+Pers+Prt+Cond+Pl1+Neg:pol%+nuksime GI ;
+Pers+Prt+Cond+Pl2+Neg:pol%+nuksite GI ;
+Pers+Prt+Cond+Pl3+Neg:pol%+nuksid GI ;
    +Pers+Prt+Quot+Neg:pol%+nuvat GI ;
   +Impers+Prs+Ind+Neg:pol%+da GI ;  
  +Impers+Prs+Cond+Neg:pol%+daks GI ;
  +Impers+Prs+Quot+Neg:pol%+davat GI ;
   +Impers+Prt+Ind+Neg:pol%+dud GI ;
  +Impers+Prt+Cond+Neg:pol%+duks GI ;    ! almost non-attested form (by Google search) ... 


! VVS 36   minema
! (3rd grade symbols missing)
LEXICON MINEMA

               +Impers:min%.%+d IMPERS_DUD ;        ! mindud
               +Impers:min%.%{W%}%+ IMPERS_AKSE ;   ! minnakse
 +Impers%+Use%/NotNorm:min%.%+d IMPERS_AKSE ;       ! mindakse
                 +Pers:mine%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :min%.%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:min%.%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:läi%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:läk%+s IND_PAST ;    ! indicative past1

   +Pers+Prs+Imprt+Sg2:mine%{W%}%+ GI ;
     +Pers+Prs+Ind+Neg:lähe%{W%}%+ GI ;
 +Pers+Prs+Ind+Sg1+Aff:lähe%{W%}%+n GI ;
 +Pers+Prs+Ind+Sg2+Aff:lähe%{W%}%+d GI ;
 +Pers+Prs+Ind+Sg3+Aff:lähe%{W%}%+b GI ;
 +Pers+Prs+Ind+Pl1+Aff:lähe%{W%}%+me GI ;
+Pers+Prs+Ind+Pl1+Aff%+Use%/Rare:läh%{W%}%+me GI ;    ! more colloquial...
 +Pers+Prs+Ind+Pl2+Aff:lähe%{W%}%+te GI ;
 +Pers+Prs+Ind+Pl3+Aff:lähe%{W%}%+vad GI ;
             +Pers+Prs:lähe%{W%}%+ CONDITIONAL ;



! VVS 37   laama, keema, leema, naima, võima, näima
LEXICON KEEMA

               +Impers:%+d IMPERS_DUD ;    ! keedud
               +Impers:%+d IMPERS_AKSE ;   ! keedakse
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%{W%}%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 

! VVS 37   saama, jääma
LEXICON SAAMA !!= @CODE@ is a lexicon for saama-type words.

               +Impers:%+d IMPERS_DUD ;    ! saadud
               +Impers:%+d IMPERS_AKSE ;   ! saadakse
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%{W%}%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7 IND_PAST ;    ! indicative past1
! +Pers+Prt+Ind+Sg3+Aff:%+i GI ;    ! sai
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 

! VVS 38   viima, käima, pooma, müüma
LEXICON VIIMA

               +Impers:%+d IMPERS_DUD ;        ! viidud
               +Impers:%{W%}%+ IMPERS_AKSE ;   ! viiakse
 +Impers%+Use%/NotNorm:%+d IMPERS_AKSE ;       ! viidakse
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%{W%}%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 

! VVS 38   jooma, looma, tooma, lööma, sööma
LEXICON TOOMA

               +Impers:%+d IMPERS_DUD ;        ! joodud
               +Impers:%{W%}%+ IMPERS_AKSE ;   ! juuakse
 +Impers%+Use%/NotNorm:%+d IMPERS_AKSE ;       ! joodakse
                 +Pers:%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:%{W%}%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:%{W%}%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7 IND_PAST ;    ! indicative past
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! 

! VVS 27 ajama; almost like VEDAMA
LEXICON AJAMA

                      :%{W%} IMPERS ;    
                 +Pers:a%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :a%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:a%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:a%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:a%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:a%+ IND_COND_PRS ;    ! 

! VVS 28 vedama, pügama, pidama
LEXICON VEDAMA

                      :e%{W%} IMPERS ;    
                 +Pers:a%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :a%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:a%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:a%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:a%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! 

! VVS 28 pidama-pidi
LEXICON PIDAMA

                      ! no impersonal forms
                 +Pers:a%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :a%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:a%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:a%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:%+I7 IND_PAST ;    ! indicative past1
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! 

! VVS 28 tegema, nägema
LEXICON TEGEMA

               +Impers:h%+d IMPERS_DUD ;       ! tehtud
               +Impers:h%{W%}%+ IMPERS_AKSE ;  ! tehakse
 +Impers%+Use%/NotNorm:h%+d IMPERS_AKSE ;      ! tehtakse
                 +Pers:ge%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :h%+ INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:h%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:i%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:g%+I7 IND_PAST ;    ! indicative past1
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! 

! VVS 28 käskima
LEXICON KÄSKIMA

                      : IMPERS ;    
                 +Pers:i%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :i%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:i%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:i%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:i%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:i%{W%}%+ IND_COND_PRS ;    ! 

! VVS 28 kiskuma ; like käskima, but u instead of i
LEXICON KISKUMA

                      : IMPERS ;             ! kistud
           %+Use%/Rare:u%{W%} IMPERS ;       ! kisutud etc
                 +Pers:u%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :u%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:u%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:u%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:u%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:u%{W%}%+ IND_COND_PRS ;    ! 

! VVS 28 uskuma ; = kiskuma + parallel forms
LEXICON USKUMA

           %+Use%/Rare: IMPERS ;          ! ustud etc
                      :u%{W%} IMPERS ;    ! usutud etc
                 +Pers:u%+ SUPINE_V ;    ! -ma etc, -v, -vat
                      :u%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:u%+G7 IMPERATIVE ;    ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:u%+nu NU ;    !  NU-affixes
         +Pers+Prt+Ind:u%+I7S7 IND_PAST ;    ! indicative past
             +Pers+Prs:u%{W%}%+ IND_COND_PRS ;    ! 

LEXICON IND_COND_PRS    ! indicative, conditional present + imp sg2

            +Imprt+Sg2: GI ;
              +Ind+Neg: GI ;
          +Ind+Sg1+Aff:n GI ;
          +Ind+Sg2+Aff:d GI ;
          +Ind+Sg3+Aff:b GI ;
          +Ind+Pl1+Aff:me GI ;
          +Ind+Pl2+Aff:te GI ;
          +Ind+Pl3+Aff:vad GI ;
                      : CONDITIONAL ;

LEXICON IND_PAST    ! indicative past
                      :i MARKED_PERSON ;
                      : SG3 ;

LEXICON NU    !  NU-affixes

                      : CONDITIONAL ;
              ! +Imprt:d GI ;    ! omitted for now because it would create homonymy
              +Ind+Neg:d GI ;
                  +Prc:d GI ;
                 +Quot:vat GI ;

LEXICON IMPERATIVE    ! imperative

                      :u GI ;    ! elagu
                  +Pl1:em GI ;    ! elagem
                  +Pl2:e GI ;    ! elage

LEXICON SUPINE_V

                  +Sup:ma SUPINE ;
              +Prs+Prc:v GI ;
             +Prs+Quot:vat GI ;

LEXICON SUPINE

                  +Ill: GI ;    ! elama
                  +Ine:s GI ;    ! elamas
                  +Ela:st GI ;
                  +Tra:ks GI ;
                  +Abe:ta GI ;

LEXICON IMPERS    ! impersonal forms

               +Impers:%+T7 IMPERS_DUD ;
               +Impers:%+T7 IMPERS_AKSE ;

LEXICON IMPERS_DUD    ! impersonal forms -[td]ud

             +Prs+Cond:aks GI ;
            +Prs+Imprt:agu GI ;
          +Prs+Ind+Neg:a GI ;
              +Prs+Prc:av GI ;
             +Prs+Quot:avat GI ;
             +Prt+Cond:uks GI ;
          +Prt+Ind+Aff:i GI ;
          +Prt+Ind+Neg:ud GI ;
              +Prt+Prc:ud GI ;
                  +Sup:ama GI ;

LEXICON IMPERS_AKSE    ! impersonal form -[td0]akse

          +Prs+Ind+Aff:akse GI ;

LEXICON INFINITIVE    ! infinitive, gerund (des-form)

                  +Inf:a GI ;
                  +Ger:es GI ;

LEXICON CONDITIONAL    ! conditional present and past  

                 +Cond:ks GI ;
                 +Cond:ksi MARKED_PERSON ;

LEXICON MARKED_PERSON     ! sg3 ~ underspecified is missing from here 
                      : SG1 ;
                      : SG2 ;
                      : PL1 ;
                      : PL2 ;
                      : PL3 ;

LEXICON SG1
              +Sg1+Aff:n GI ;

LEXICON SG2
              +Sg2+Aff:d GI ;

LEXICON SG3
              +Sg3+Aff: GI ;

LEXICON SG3B
              +Sg3+Aff:b GI ;

LEXICON PL1
              +Pl1+Aff:me GI ;

LEXICON PL2
              +Pl2+Aff:te GI ;

LEXICON PL3
              +Pl3+Aff:d GI ;

LEXICON PL3VAD
              +Pl3+Aff:vad GI ;

