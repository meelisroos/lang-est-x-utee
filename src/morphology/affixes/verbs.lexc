
!! !!!Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes
! VVS type numbers = type numbers of Õigekeesussõnastik 2013

LEXICON ELAMA    ! VVS 27

                      : IMPERS ;           ! elatud
                 +Pers:%+ SUPINE_V ;       ! elama
                      :%+D7 INFINITIVE ;   ! elada
       +Pers+Prs+Imprt: IMPERATIVE ;       ! elagu
             +Pers+Prt:%+nu NU ;           ! elanud
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;   ! elas
             +Pers+Prs:%+ IND_COND_PRS ;   ! elan, elaksin

LEXICON JULGEMA   ! only julgema

                      : ELAMA ;    
           %+Use%/Rare:%{W%} IMPERS ;      ! impersonal forms  juleta...
  +Pers+Prt%+Use%/Rare:%{W%}%+nu NU ;      ! NU-affixes julenud...

LEXICON LEPPIMA   ! VVS 28

                      : LEPPIMA_STRONG ;
                      :%{W%} LEPPIMA_WEAK ;

LEXICON LEPPIMA_STRONG   ! strong grade sub-paradigm

                 +Pers:%+ SUPINE_V ;        ! leppima
                      :%+D7 INFINITIVE ;    ! leppida
       +Pers+Prs+Imprt: IMPERATIVE ;        ! leppigu
             +Pers+Prt:%+nu NU ;            ! leppinud
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! leppis

LEXICON LEPPIMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;           ! lepitud
             +Pers+Prs:%+ IND_COND_PRS ;   ! lepin, lepiksin

LEXICON LEKKIMA   ! VVS 28 & 27
                  ! like SÜTTIMA, but non-grading forms are less used

                      : LEPPIMA_STRONG ;         ! lekkima, lekib, lekitud, ...
                      :%{W%} LEPPIMA_WEAK ;      ! lekib, lekiks
           %+Use%/Rare: LEPPIMA_WEAK ;           ! lekkitud, lekkib, ...

LEXICON SÜTTIMA   ! VVS 28 & 27
                  ! like LEKKIMA, but weak grade forms are less used

                      : LEPPIMA_STRONG ;         ! süttima
                      : LEPPIMA_WEAK ;           ! süttib, süttiks
           %+Use%/Rare:%{W%} LEPPIMA_WEAK ;      ! sütitud, sütib, sütiks

LEXICON HAKKAMA    ! VVS 29 (hakkama), 30 (riidlema)

                      :%{W%} HAKKAMA_WEAK ;
                 +Pers:%+ SUPINE_V ;            ! hakkama, riidlema
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;        ! hakkas, riidles
             +Pers+Prs:%+ IND_COND_PRS ;        ! hakkan, riidlen

LEXICON HAKKAMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;            ! hakatud, riieldud etc
                      :%+D7 INFINITIVE ;    ! hakata, riielda
       +Pers+Prs+Imprt: IMPERATIVE ;        ! hakaku, riielgu
             +Pers+Prt:%+nu NU ;            ! hakanud, riielnud

LEXICON ÜTLEMA    ! only ütlema, mõtlema
                  ! parallel forms: either t disappears in weak grade, or not

                      :le HAKKAMA ;             ! ütlema, ..., öeldud, öelda, öelgu, öelnud
           %+Use%/Rare:tle%{W%} HAKKAMA_WEAK ;  ! üteldud, ütelda, ütelgu, ütelnud

LEXICON RABELEMA    ! VVS 31; = HAKKAMA + ELAMA 
                    ! assuming that rabel- is less used than rabele- ...

                      : ELAMA ;                 ! rabelema
           %+Use%/Rare:%{W%} HAKKAMA_WEAK ;     ! rabeldud, rabelda, ...

LEXICON KÄSKIMA     ! only käskima

                      :i LEPPIMA_STRONG ;         ! käskima etc
                      : IMPERS ;                  ! kästud
             +Pers+Prs:i%{W%}%+ IND_COND_PRS ;    ! käsin etc

LEXICON KISKUMA     ! only kiskuma 

                      :u LEPPIMA_STRONG ;         ! kiskuma etc
                      : IMPERS ;                  ! kistud
           %+Use%/Rare:u%{W%} IMPERS ;            ! kisutud etc
             +Pers+Prs:u%{W%}%+ IND_COND_PRS ;    ! kisun etc

LEXICON USKUMA      ! only uskuma 

           %+Use%/Rare: IMPERS ;          ! ustud etc
                      :u LEPPIMA ;        ! uskuma, usutud etc

! ---------------
LEXICON NAERMA      ! VVS 32, 33  naerma, seisma etc (24 words)

                      : C_COMMON ;              ! naerda, seista, naergu, seisku, naernud, seisnud
                      : IMPERS ;                ! naerdud, seistud
                 +Pers:%+ SUPINE_V ;            ! naerma, seisma
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;        ! naeris, seisis
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;  ! naerab, seisab

LEXICON KUULMA      ! VVS 32, 33  kuulma, tõusma etc; (7 words) like NAERMA, but kuuleb (not kuulab)

                      : C_COMMON ;             ! kuulda, tõusta, kuulgu, tõusku, kuulnud, tõusnud
                      : IMPERS ;               ! kuuldud, tõustud
                 +Pers:%+ SUPINE_V ;           ! kuulma, tõusma
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;       ! kuulis, tõusis
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ; ! kuuleb, tõuseb

LEXICON PESEMA    ! only pesema

                      : C_COMMON ;            ! pesta, pesku, pesnud
                      : IMPERS ;              ! pestud
                 +Pers:e%+ SUPINE_V ;         ! pesema
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;      ! pesi
             +Pers+Prs:e%+ IND_COND_PRS ;     ! peseb

LEXICON KUSEMA    ! only kusema

                      : PESEMA ;    
        %+Use%/NotNorm:e%+D7 INFINITIVE ;         ! kuseda
+Pers+Prs+Imprt%+Use%/NotNorm:e IMPERATIVE ;      ! kusegu
+Pers+Prt+Ind%+Use%/NotNorm:e%+I7S7 IND_PAST ;    ! kuses

LEXICON KAITSMA    ! only  kaitsma, maitsma

                      : C_COMMON ;            ! kaitsta, kaitsku, kaitsnud
                      : IMPERS ;              ! kaitstu
                 +Pers:%+ SUPINE_V ;          ! kaitsma
         +Pers+Prt+Ind:e%+I7S7 IND_PAST ;     ! kaitses
             +Pers+Prs:e%+ IND_COND_PRS ;     ! kaitseb

LEXICON SAATMA      ! VVS 34, 35   söötma, nutma

                      : C_COMMON ;              ! sööta, nutta, söötke, nutke, söötnud, nutnud
                      :%{W%} IMPERS ;           ! söödetud, nutetud
                 +Pers:%+ SUPINE_V ;            ! söötma, nutma
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;        ! söötis, nuttis
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;  ! söödab, nutab

LEXICON JOOKSMA     ! only jooksma

                      :%{W%} IMPERS ;           ! impersonal forms joostakse, ... 
           %+Use%/Rare: IMPERS ;                ! impersonal forms jookstakse,...
                 +Pers:%+ SUPINE_V ;            ! jooksma
                      :%{W%}%+D7 INFINITIVE ;   ! joosta, joostes 
           %+Use%/Rare:%+D7 INFINITIVE ;        ! jooksta, jookstes
       +Pers+Prs+Imprt: IMPERATIVE ;            ! jooksku
+Pers+Prs+Imprt%+Use%/Rare:%{W%} IMPERATIVE ;   ! joosku
             +Pers+Prt:%+nu NU ;                ! jooksnud
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;        ! jooksis, jooksin
             +Pers+Prs:e%+ IND_COND_PRS ;       ! jookseb

LEXICON LASKMA    ! only laskma

                      : C_COMMON ;                ! lasta, lasku, lasknud
                      : IMPERS ;                  ! impersonal forms lastakse, ...
                 +Pers:%+ SUPINE_V ;              ! laskma
+Pers+Prt+Ind%+Use%/Rare:%+I7S7 IND_PAST ;        ! indicative past laskis, ...
         +Pers+Prt+Ind:%{W%}%+I7S7 IND_PAST ;     ! indicative past lasi, ...
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! laseb

LEXICON LÕPMA      ! only lõpma; almost like SAATMA

                      : C_COMMON ;                ! lõppa, lõpku, lõpnud
                      :%{W%} IMPERS ;             ! lõpetud
                 +Pers:%+ SUPINE_V ;              ! lõpma
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;          ! lõppes
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! lõpeb

LEXICON KATMA      ! VVS 35   katma kütma matma võtma jätma
                   ! almost like SAATMA, but IMPERS are even weaker grade

                      :t C_COMMON ;          ! kütta
                      :%{W%} IMPERS ;        ! köetud
                 +Pers:t%+ SUPINE_V ;        ! kütma
         +Pers+Prt+Ind:t%+I7S7 IND_PAST ;    ! küttis
             +Pers+Prs:a%+ IND_COND_PRS ;    ! kütab

LEXICON C_COMMON  ! common to almost all words of types 32-35 (supine stem ends with C)

                      :%+D7 INFINITIVE ;    ! naerda, seista, sööta, nutta, kütta
       +Pers+Prs+Imprt: IMPERATIVE ;        ! naergu, seisku, söötku, nutku, kütku
             +Pers+Prt:%+nu NU ;            ! kütnud


! ---------------
LEXICON TULEMA     ! only tulema, panema, surema

               +Impers:%.%+d IMPERS_DUD ;        ! surdud
               +Impers:%.%{W%}%+ IMPERS_AKSE ;   ! surrakse
 +Impers%+Use%/NotNorm:%.%+d IMPERS_AKSE ;       ! surdakse
                 +Pers:e%+ SUPINE_V ;            ! surema
                      :%.%+ INFINITIVE ;         ! surra
       +Pers+Prs+Imprt:%{W%} IMPERATIVE ;        ! surgu
             +Pers+Prt:%+nu NU ;                 ! surnud
         +Pers+Prt+Ind:%+I7 IND_PAST ;           ! suri, surin, ...
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;   ! sureb

LEXICON OLEMA    ! only olema (3rd grade symbols missing here)

               +Impers:ol%.%+d IMPERS_DUD ;        ! oldud
               +Impers:ol%.%{W%}%+ IMPERS_AKSE ;   ! ollakse
 +Impers%+Use%/NotNorm:ol%.%+d IMPERS_AKSE ;       ! oldakse
                 +Pers:ole%+ SUPINE_V ;            ! olema
                      :ol%.%+ INFINITIVE ;         ! olla
       +Pers+Prs+Imprt:ol IMPERATIVE ;             ! olgu
             +Pers+Prt:ol%+nu NU ;                 ! olnud
         +Pers+Prt+Ind:ol%+I7 IND_PAST ;           ! oli, olin, ...

   +Pers+Prs+Imprt+Sg2:ole%+ GI ;
     +Pers+Prs+Ind+Neg:ole%+ GI ;
 +Pers+Prs+Ind+Sg1+Aff:ole%+n GI ;
 +Pers+Prs+Ind+Sg2+Aff:ole%+d GI ;
 +Pers+Prs+Ind+Sg3+Aff:on GI ;
 +Pers+Prs+Ind+Pl1+Aff:ole%+me GI ;
 +Pers+Prs+Ind+Pl2+Aff:ole%+te GI ;
 +Pers+Prs+Ind+Pl3+Aff:on GI ;
             +Pers+Prs:ole%{W%}%+ CONDITIONAL ;

! + negative forms...
     +Pers+Prs+Ind+Neg:pole%+ GI ;    ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%+ksin GI ;
+Pers+Prs+Cond+Sg2+Neg:pole%+ksid GI ;
    +Pers+Prs+Cond+Neg:pole%+ks GI ;
+Pers+Prs+Cond+Pl1+Neg:pole%+ksime GI ;
+Pers+Prs+Cond+Pl2+Neg:pole%+ksite GI ;
+Pers+Prs+Cond+Pl3+Neg:pole%+ksid GI ;
    +Pers+Prs+Quot+Neg:pole%+vat GI ;
     +Pers+Prt+Ind+Neg:pol%+nud GI ;
+Pers+Prt+Cond+Sg1+Neg:pol%+nuksin GI ;
+Pers+Prt+Cond+Sg2+Neg:pol%+nuksid GI ;
    +Pers+Prt+Cond+Neg:pol%+nuks GI ;
+Pers+Prt+Cond+Pl1+Neg:pol%+nuksime GI ;
+Pers+Prt+Cond+Pl2+Neg:pol%+nuksite GI ;
+Pers+Prt+Cond+Pl3+Neg:pol%+nuksid GI ;
    +Pers+Prt+Quot+Neg:pol%+nuvat GI ;
   +Impers+Prs+Ind+Neg:pol%+da GI ;  
  +Impers+Prs+Cond+Neg:pol%+daks GI ;
  +Impers+Prs+Quot+Neg:pol%+davat GI ;
   +Impers+Prt+Ind+Neg:pol%+dud GI ;
  +Impers+Prt+Cond+Neg:pol%+duks GI ;    ! almost non-attested form (by Google search) ... 

LEXICON MINEMA    ! only minema (3rd grade symbols missing here)

               +Impers:min%.%+d IMPERS_DUD ;        ! mindud
               +Impers:min%.%{W%}%+ IMPERS_AKSE ;   ! minnakse
 +Impers%+Use%/NotNorm:min%.%+d IMPERS_AKSE ;       ! mindakse
                 +Pers:mine%+ SUPINE_V ;            ! minema
                      :min%.%+ INFINITIVE ;         ! minna
       +Pers+Prs+Imprt:min%.%{W%} IMPERATIVE ;      ! mingu
             +Pers+Prt:läi%+nu NU ;                 ! läinud
         +Pers+Prt+Ind:läk%+s IND_PAST ;            ! läks, läksin etc

   +Pers+Prs+Imprt+Sg2:mine%{W%}%+ GI ;
     +Pers+Prs+Ind+Neg:lähe%{W%}%+ GI ;
 +Pers+Prs+Ind+Sg1+Aff:lähe%{W%}%+n GI ;
 +Pers+Prs+Ind+Sg2+Aff:lähe%{W%}%+d GI ;
 +Pers+Prs+Ind+Sg3+Aff:lähe%{W%}%+b GI ;
 +Pers+Prs+Ind+Pl1+Aff:lähe%{W%}%+me GI ;
+Pers+Prs+Ind+Pl1+Aff%+Use%/Rare:läh%{W%}%+me GI ;    ! lähme more colloquial...
 +Pers+Prs+Ind+Pl2+Aff:lähe%{W%}%+te GI ;
 +Pers+Prs+Ind+Pl3+Aff:lähe%{W%}%+vad GI ;
             +Pers+Prs:lähe%{W%}%+ CONDITIONAL ;

! ---------------
LEXICON KEEMA   ! VVS 37   laama, keema, leema, naima, võima, näima

                      : VV_COMMON ;
               +Impers:%+d IMPERS_DUD ;     ! keedud
               +Impers:%+d IMPERS_AKSE ;    ! keedakse
                      :%+D7 INFINITIVE ;    ! keeda
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;    ! kees

LEXICON SAAMA !!= @CODE@ is a lexicon for saama-type words. ! VVS 37   saama, jääma

                      : VV_COMMON ;
               +Impers:%+d IMPERS_DUD ;     ! saadud
               +Impers:%+d IMPERS_AKSE ;    ! saadakse
                      :%+D7 INFINITIVE ;    ! saada
         +Pers+Prt+Ind:%+I7 IND_PAST ;      ! sai

LEXICON VIIMA     ! VVS 38   viima, käima, pooma, müüma

                      : VV_COMMON ;
               +Impers:%+d IMPERS_DUD ;        ! viidud
               +Impers:%{W%}%+ IMPERS_AKSE ;   ! viiakse
 +Impers%+Use%/NotNorm:%+d IMPERS_AKSE ;       ! viidakse
                      :%+ INFINITIVE ;         ! viia
         +Pers+Prt+Ind:%+I7S7 IND_PAST ;       ! viis

LEXICON TOOMA     ! VVS 38   jooma, looma, tooma, lööma, sööma

                      : VV_COMMON ;
               +Impers:%+d IMPERS_DUD ;        ! joodud
               +Impers:%{W%}%+ IMPERS_AKSE ;   ! juuakse
 +Impers%+Use%/NotNorm:%+d IMPERS_AKSE ;       ! joodakse
                      :%+ INFINITIVE ;         ! juua
         +Pers+Prt+Ind:%+I7 IND_PAST ;         ! jõi
 
LEXICON VV_COMMON  ! common to all 17 words of types 37, 38 (supine stem ends with VV)

                 +Pers:%+ SUPINE_V ;             ! keema, saama etc
       +Pers+Prs+Imprt:%{W%} IMPERATIVE ;        ! keegu, saagu etc
             +Pers+Prt:%{W%}%+nu NU ;            ! keenud, saanud etc
             +Pers+Prs:%{W%}%+ IND_COND_PRS ;    ! keeb, saab etc

! ---------------

LEXICON AJAMA      ! only ajama; almost like VEDAMA

                      :%{W%} IMPERS ;        ! aetud
                 +Pers:a%+ SUPINE_V ;        ! ajama
                      :a%+D7 INFINITIVE ;    ! ajada
       +Pers+Prs+Imprt:a IMPERATIVE ;        ! ajagu
             +Pers+Prt:a%+nu NU ;            ! ajanud
         +Pers+Prt+Ind:a%+I7S7 IND_PAST ;    ! ajas
             +Pers+Prs:a%+ IND_COND_PRS ;    ! ajab

LEXICON VEDAMA     ! VVS 28 vedama, pügama, pidama

                      :e%{W%} IMPERS ;       ! veetud
                 +Pers:a%+ SUPINE_V ;        ! vedama
                      :a%+D7 INFINITIVE ;    ! vedada
       +Pers+Prs+Imprt:a IMPERATIVE ;        ! vedagu
             +Pers+Prt:a%+nu NU ;            ! vedanud
         +Pers+Prt+Ind:a%+I7S7 IND_PAST ;    ! vedas
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! veab

LEXICON PIDAMA     ! only pidama-pidi

                      ! no impersonal forms
                 +Pers:a%+ SUPINE_V ;        ! -ma etc, -v, -vat
                      :a%+D7 INFINITIVE ;    ! infinitive, gerund (des-form)
       +Pers+Prs+Imprt:a IMPERATIVE ;        ! -gu/ku, -ge/ke, -gem/kem
             +Pers+Prt:a%+nu NU ;            !  NU-affixes
         +Pers+Prt+Ind:%+I7 IND_PAST ;       ! pidi
             +Pers+Prs:a%{W%}%+ IND_COND_PRS ;    ! peab

LEXICON TEGEMA     ! only tegema, nägema

               +Impers:h%+d IMPERS_DUD ;       ! tehtud
               +Impers:h%{W%}%+ IMPERS_AKSE ;  ! tehakse
 +Impers%+Use%/NotNorm:h%+d IMPERS_AKSE ;      ! tehtakse
                 +Pers:ge%+ SUPINE_V ;         ! tegema
                      :h%+ INFINITIVE ;        ! teha
       +Pers+Prs+Imprt:h IMPERATIVE ;          ! tehku
             +Pers+Prt:i%+nu NU ;              ! teinud
         +Pers+Prt+Ind:g%+I7 IND_PAST ;        ! tegi
             +Pers+Prs:e%{W%}%+ IND_COND_PRS ;    ! teeb

LEXICON IND_COND_PRS    ! indicative, conditional present + imp sg2

            +Imprt+Sg2: GI ;
              +Ind+Neg: GI ;
                  +Ind: SG1 ;
                  +Ind: SG2 ;
                  +Ind: SG3B ;
                  +Ind: PL1 ;
                  +Ind: PL2 ;
                  +Ind: PL3VAD ;
                      : CONDITIONAL ;
            
LEXICON IND_PAST    ! indicative past
                      :i MARKED_PERSON ;
                      : SG3 ;

LEXICON NU    !  NU-affixes

                      : CONDITIONAL ;
              ! +Imprt:d GI ;    ! omitted for now because it would create homonymy
              +Ind+Neg:d GI ;
                  +Prc:d GI ;
                 +Quot:vat GI ;

LEXICON IMPERATIVE    ! imperative

                      :%+G7u GI ;    ! elagu
                  +Pl1:%+G7em GI ;    ! elagem
                  +Pl2:%+G7e GI ;    ! elage

LEXICON SUPINE_V

                  +Sup:ma SUPINE ;
              +Prs+Prc:v GI ;
             +Prs+Quot:vat GI ;

LEXICON SUPINE

                      : ILLATIVE ;    ! elama
                      :s INESSIVE ;    ! elamas
                      :st ELATIVE ;
                      :ks TRANSLATIVE ;
                      :ta ABESSIVE ;

LEXICON IMPERS    ! impersonal forms

               +Impers:%+T7 IMPERS_DUD ;
               +Impers:%+T7 IMPERS_AKSE ;

LEXICON IMPERS_DUD    ! impersonal forms -[td]ud

             +Prs+Cond:aks GI ;
            +Prs+Imprt:agu GI ;
          +Prs+Ind+Neg:a GI ;
              +Prs+Prc:av GI ;
             +Prs+Quot:avat GI ;
             +Prt+Cond:uks GI ;
          +Prt+Ind+Aff:i GI ;
          +Prt+Ind+Neg:ud GI ;
              +Prt+Prc:ud GI ;
                  +Sup:ama GI ;

LEXICON IMPERS_AKSE    ! impersonal form -[td0]akse

          +Prs+Ind+Aff:akse GI ;

LEXICON INFINITIVE    ! infinitive, gerund (des-form)

                  +Inf:a GI ;
                  +Ger:es GI ;

LEXICON CONDITIONAL    ! conditional present and past  

                 +Cond:ks GI ;
                 +Cond:ksi MARKED_PERSON ;

LEXICON MARKED_PERSON     ! sg3 ~ underspecified is missing from here 
                      : SG1 ;
                      : SG2 ;
                      : PL1 ;
                      : PL2 ;
                      : PL3 ;

LEXICON SG1
              +Sg1+Aff:n GI ;

LEXICON SG2
              +Sg2+Aff:d GI ;

LEXICON SG3
              +Sg3+Aff: GI ;

LEXICON SG3B
              +Sg3+Aff:b GI ;

LEXICON PL1
              +Pl1+Aff:me GI ;

LEXICON PL2
              +Pl2+Aff:te GI ;

LEXICON PL3
              +Pl3+Aff:d GI ;

LEXICON PL3VAD
              +Pl3+Aff:vad GI ;

