! inflectional types and analogue rules for nouns, adjectives and other declinables
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes

!---------------
! inflectional types
!---------------

!---------------
! exceptional inflectional types, i.e. covering very few words each
!---------------

LEXICON ARGPÜKS   ! some compound words ending with püks (argpüks, vesipüks etc)
                  ! and declined not exactly like püksid (e.g. karupüksid, sukkpüksid) 
                  ! almost like TAUD, but has some less used pl forms
: TAUD ;
%+Use%/Rare:%{W%}%+te N_PL_SUBPARA ;           ! argpükste etc


LEXICON KALLIS    !  VVS 7, 5 only kallis, kaunis unstable (closed) type; 

+Sg+Nom:s%{W%} GI ;                      ! kallis
+Sg+Par:s%{W%}%+t GI ;                   ! kallist
:%+te N_PL_SUBPARA ;                     ! kallite etc 
%+Use%/Rare:s%{W%}%+te N_PL_SUBPARA ;    ! kalliste etc 
: 2C_STRONG ;                            ! kalli etc, kalleid, kalleil etc

LEXICON KAKS                  ! only üks, kaks

+Sg+Nom:ks GI ;                                ! kaks
:he N_SG_SUBPARA ;                             ! kahe etc 
+Sg+Par:h%+t GI ;                              ! kaht
+Sg+Par%+Use%/Rare:h%+te GI ;                  ! kahte
+Sg+Ill:he%+sse GI ;                           ! kahesse 
:hte%+de N_PL_SUBPARA ;                         ! kahtede etc 
+Pl+Par:hte%+sid GI ;                          ! kahtesid

LEXICON KEVAD              ! only küps, kevad, = REDEL, but stem vowel is e

+Sg+Nom:%{no.apo%} GI ;              ! kevad
:e 2C ;                              ! kevade etc

LEXICON KES       ! kes, mis 

+Sg+Nom:s #;
+Sg+Ade%+Use%/Rare:%+l #;                           ! kel, mil 
: KEMISETO ;
:lle%+de N_PL_SUBPARA ;                         ! kellede etc 

LEXICON SEE       ! see 

+Sg+Nom:e GI ;
+Sg+Ill%+Use%/Rare:%+sse GI ;   ! sesse
+Sg+Ine%+Use%/Rare:%+s #;   ! ses
+Sg+Ela%+Use%/Rare:%+st GI ;  ! sest
+Sg+Ade%+Use%/Rare:%+l GI ;   ! sel
+Sg+Abl%+Use%/Rare:%+lt GI ;  ! selt	
+Sg+Tra%+Use%/Rare:%+ks GI ;  ! seks
: KEMISETO ;

LEXICON TOO       ! too 

+Sg+Nom:o GI ;
+Sg+Ill%+Use%/Rare:%+sse GI ;   ! tosse
+Sg+Ine%+Use%/Rare:%+s #;   ! tos
+Sg+Ela%+Use%/Rare:%+st GI ;  ! tost
+Sg+Ade%+Use%/Rare:%+l GI ;   ! tol
: KEMISETO ;

LEXICON KEMISETO      ! kes, mis, see, too common cont. lexicon

+Sg:lle STANDARD_GEN_ET_AL ;                      ! kelle, tolle etc , excl Pl Nom
+Sg+Par:%+da GI ;                               ! keda, toda etc

LEXICON NEED       ! need 

+Pl+Nom:e%+d GI ;           ! need
: NENO ;

LEXICON NEMAD       ! nemad 

+Pl+Nom+Emph:ema%+d GI ;           ! nemad
+Pl+Nom:a%+d GI ;           ! nad
:e NENO ;

LEXICON NOOD       ! nood 

+Pl+Nom:o%+d GI ;           ! nood
: NENO ;

LEXICON NENO       ! need & nood common cont. lexicon

:n%+de N_PL_SUBPARA ;      ! nende etc 
+Pl+Par:%+id GI ;           ! neid
+Pl:i STANDARD_LOCATIVES ;     ! neil etc
+Pl+Trm:n%+de%+ni GI ;	  ! nendeni
+Pl+Ess:n%+de%+na GI ;	  ! nendena
+Pl+Abe:n%+de%+ta GI ;	  ! nendeta
+Pl+Com:n%+de%+ga GI ;    ! nendega
%+Use%/Rare:n%+de N_VOWEL_PL_SUBPARA ;      ! nendel etc 

LEXICON MASA       ! ma & sa 

+Sg+Nom+Emph:ina GI ;    ! mina, sina
+Sg+Nom:a #;                  ! ma, sa
+Sg+Gen+Emph:inu GI ;                  ! minu, sinu
+Sg+Gen:u #;                  ! mu, su
+Sg+Par:in%+d GI ;                               ! mind, sind
+Sg+Ill:u%+sse GI ;   ! musse
+Sg+Ine:u%+s #;     ! mus
+Sg+Ela:u%+st GI ;
+Sg+All:ul%+le GI ;	! mulle
+Sg+Ade:u%+l GI ;	
+Sg+Abl:u%+lt GI ;	
+Sg+Com:u%+ga #;     ! muga
+Sg+Emph:inu STANDARD_LOCATIVES ;                      ! minul, sinul
+Sg+Tra:inu%+ks GI ;
+Sg+Trm:inu%+ni GI ;	  ! minuni
+Sg+Ess:inu%+na GI ;	  ! minuna
+Sg+Abe:inu%+ta GI ;	  ! minuta
+Pl+Com+Emph:inu%+ga GI ;      ! minuga

LEXICON TEMA       ! ta 

+Sg+Nom+Emph:ema GI ;    ! tema
+Sg+Nom:a #;                  ! ta
+Sg+Gen+Emph:ema GI ;                  ! tema
+Sg+Gen:a #;                  ! ta
+Sg+Par:e%+da GI ;                               ! teda
+Sg+Emph:ema STANDARD_LOCATIVES ;                      ! temal etc
+Sg+Tra:ema%+ks GI ;
+Sg+Trm:ema%+ni GI ;	  ! temani
+Sg+Ess:ema%+na GI ;	  ! temana
+Sg+Abe:ema%+ta GI ;	  ! temata
+Pl+Com:ema%+ga GI ;      ! temaga
+Sg:a STANDARD_LOCATIVES ;                      ! tal etc

LEXICON KÕIK

+Sg+Nom: #;
+Sg:e%{W%} STANDARD_GEN_ET_AL ;                      ! kõige etc
+Sg+Par:e GI ;                              ! kõike
+Pl+Nom: #;                            ! kõik
+Pl%+Use%/Rare:i%+de STANDARD_GEN_ET_AL ;                      ! kõikide etc
+Pl:i%{W%} STANDARD_GEN_ET_AL ;                      ! kõigi etc

LEXICON METE       ! me & te 

+Pl+Nom+Emph:ie GI ;    ! meie, teie
+Pl+Nom: #;                  ! me, te
+Pl+Gen+Emph:ie GI ;    ! meie, teie
+Pl+Gen: #;                  ! me, te
+Pl+Par:i%+d GI ;                               ! meid, teid
+Pl:i STANDARD_LOCATIVES ;                      ! meil, teil etc
+Pl+Tra:ie%+ks GI ;
+Pl+Trm:ie%+ni GI ;	  ! meieni
+Pl+Ess:ie%+na GI ;	  ! meiena
+Pl+Abe:ie%+ta GI ;	  ! meieta
+Pl+Com:ie%+ga GI ;    ! meiega

LEXICON ISE       ! ise 

+Sg+Nom:ise GI ;                  ! ise
+Sg:enese REGULAR_GEN_ET_AL ;                      ! enese, enesel etc
+Sg:enda REGULAR_GEN_ET_AL ;                      ! enda, endal etc
+Sg+Par:ennas%+t GI ;                               
+Sg+Par:en%+d GI ;                               
+Sg+Ill:enese%+sse GI ;                            
+Sg+Ill:enda%+sse GI ;                           
+Pl:enes%+te REGULAR_GEN_ET_AL ;
+Pl:end%+i REGULAR_GEN_ET_AL ;

LEXICON OMAENESE       !  

+Sg+Gen%+Use%/Rare:enese GI ;                      ! enese, enesel etc
+Sg+Gen:enda GI ;                      ! enda, endal etc
+Pl+Gen:enes%+te GI ;
+Pl+Gen%+Use%/Rare:end%+i GI ;

LEXICON KEEGI    ! only keegi; (kes+gi); nonstandard parallel forms kellegil etc like REDEL

+Sg+Nom:e GI ;
: KEMISETO ;
%+Use%/NotNorm:llegi STANDARD_SEM_CASES ;  ! kellegis, 

LEXICON MISKI    ! only miski; mis+gi; nonstandard parallel forms millegil etc; miskid etc like REDEL

: KES ;
%+Use%/NotNorm:llegi STANDARD_SEM_CASES ;  ! millegis, millegil etc
%+Use%/NotNorm:skI5 2C ;                         ! miskid, miskite etc 


LEXICON KUMBKI    ! only kumbki; kumb like PIIM +gi; nonstandard parallel forms kummagil etc like REDEL

+Sg+Nom:%{no.apo%} GI ;
:%{sg.a%} 1C ;
%+Use%/NotNorm:agI5%{W%} 2C ;  ! kummagis, kummagil etc
  
LEXICON KOHUS                 ! only kohus-kohtu; like HOOLAS

+Sg+Nom:s%{W%} GI ;                           ! kohus
: 2C_EXCL_SG_PAR ;                            ! kohtu etc  
+Sg+Par:%{W%}%+t GI ;                         ! kohut
+Sg+Par%+Use%/Rare:%+t GI ;                   ! kohtut

LEXICON KURAT    ! only kurat; like redel
   
+Sg+Nom:t GI ;                       ! kurat
:dI5 2C ;                            ! kuradi etc

LEXICON KÕRB   ! only kõrb

+Sg+Nom: GI ;                                  ! kõrb
:%+e N_SG_SUBPARA ;                            ! kõrbe etc 
+Sg+Par:e GI ;                                 ! kõrbe
+Sg+Par%+Use%/Rare:e%+t GI ;                   ! kõrbet
+Sg+Ill%+Use%/Rare:e GI ;                      ! kõrbe
+Sg+Ill:e%{W%}%+sse GI ;                       ! kõrbesse 
%+Use%/Rare:e%{W%}%+de N_PL_SUBPARA ;          ! kõrbede etc 
:e%+te N_PL_SUBPARA ;                          ! kõrbete etc 
+Pl+Par:e%+id GI ;                             ! kõrbeid 
+Pl+Par%+Use%/Rare:i GI ;                      ! kõrbi
+Pl+Par%+Use%/Rare:e%+sid GI ;                 ! kõrbesid

LEXICON KÖÖMEN               ! only köömen; like KEVAD

+Sg+Nom:%{no.apo%} GI ;                        ! köömen
:e 2C ;                                        ! köömne etc  
+Sg+Par%+Use%/CommonNotNorm:%+t GI ;           ! kööment

LEXICON LAPS               ! only laps, uks; notice the weird (?) use of {W}

+Sg+Nom: GI ;                                 ! laps
:e N_SG_SUBPARA ;                             ! lapse etc 
+Sg+Par:%{W%}%+t GI ;                              ! last
+Sg+Ill:e%+sse GI ;                           ! lapsesse 
:%{W%}%+te N_PL_SUBPARA ;                          ! laste etc 
+Pl+Par:%+i GI ;                              ! lapsi

LEXICON LÕUNA                 ! only lõuna; almost like haare

+Sg+Nom:%{W%} GI ;                      ! lõuna
+Sg+Par:%{W%}%+t GI ;                   ! lõunat
: 2C_EXCL_SG_PAR ;                      ! lõuna etc


LEXICON MANNER    ! VVS 3 only manner, tanner
                  ! almost = VAHER, but stem vowel is i

+Sg+Nom:%{W%} GI ;                     ! manner
:I5 2C ;                               ! mandri etc

LEXICON MEES                  ! only mees

+Sg+Nom:es GI ;                                ! mees
:he N_SG_SUBPARA ;                             ! mehe etc 
+Sg+Par:es%+t GI ;                             ! meest
+Sg+Ill:he%+sse GI ;                           ! mehesse 
:es%+te N_PL_SUBPARA ;                         ! meeste etc 
+Pl+Par:h%+i GI ;                              ! mehi

LEXICON MÕNI    ! only mõni; similar to MERI

+Sg+Nom:i GI ;
:e N_SG_SUBPARA ;                        ! mõne etc
+Sg+Par%+Use%/Rare:%+d GI ;                         ! mõnd
+Sg+Par:+da GI ;                     ! actually, mõnda is the preferred form...
+Sg+Ill:+da GI ;                     ! actually, mõnda is the preferred form...
+Sg+Ill%+Use%/Rare:e%{W%} SG_ILL ;     ! mõnesse 
:%+e%+de N_PL_SUBPARA ;                  ! mõnede etc
+Pl+Par:e%+sid GI ;                      ! mõnesid


LEXICON NAINE   ! only naine; much like OLULINE
                  
+Sg+Nom: GI ;
: NE_S ;
+Sg+Ill:E1%+sse GI ;             ! naisesse
+Pl+Par:%+I1 GI ;                           ! naisi
%+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;       ! naisil etc

LEXICON NIIN    ! only niin; almost like KEEL, but some forms are more regular

+Sg+Nom: GI ;                                 ! niin
:e%{W%} N_SG_SUBPARA ;                        ! niine etc 
+Sg+Par:%+t GI ;                              ! niint
+Sg+Ill:e%{W%}%+sse GI ;                      ! niinesse 
:%+te N_PL_SUBPARA ;                          ! niinte etc 
! +N%+Use%/Hyp:%+i N_VOWEL_PL_SUBPARA ;       ! impossible for rarer words
+Pl+Par:e%+sid GI ;                           ! NB! niinesid (not niini)

LEXICON PARAS      ! only paras; like LOHAKAS

+Sg+Nom:s GI ;                         ! paras
:ja 2C ;                               ! paraja etc

LEXICON PEEN    ! only peen, tüün; almost like KEEL, but some forms are more regular

+Sg+Nom: GI ;                                  ! peen
:%+e N_SG_SUBPARA ;                            ! peene etc 
+Sg+Par:%+t GI ;                               ! peent
+Sg+Par%+Use%/NotNorm:e%+t GI ;                ! peenet
+Sg+Ill%+Use%/Rare:%+de GI ;                   ! peende
+Sg+Ill:e%{W%}%+sse GI ;                       ! peenesse 
:%+te N_PL_SUBPARA ;                           ! peente etc 
%+Use%/NotNorm:e%+te N_PL_SUBPARA ;            ! peenete etc 
+Pl+Par%+Use%/CommonNotNorm:e%+id GI ;         ! NB! peeneid 
+Pl+Par:i GI ;                                 ! peeni
%+Use%/Hyp:%{W%}%+i N_VOWEL_PL_SUBPARA ;       ! peenil etc

LEXICON PÄIKE  ! only päike(ne), äike(ne)  VVS 10

: XÄIKE ;
+Pl+Par:kese%+id GI ;                      ! päikeseid
+Pl+Par%+Use%/Rare:kse%+id GI ;            ! päikseid

LEXICON REHI   ! rehi, ruhi

: EIT ;
+Sg+Par%+Use%/Rare:%{W%}%+t #;   ! reht, ruht

LEXICON RIND   ! only rind, silm

: PIIM ;
+Pl+Gen%+Use%/Rare:%+e #;      ! rinde

LEXICON JALG   ! only jalg

: PIIM ;
%+Use%/Rare:%+e N_VOWEL_PL_SUBPARA ;      ! jalge

LEXICON RUBLA  ! about 20 words 

: PERE ;
+Sg+Par%+Use%/Rare: GI ;            ! (viis) rubla

LEXICON SADA    ! only sada

: KAVA ;
+Sg+Par%+Use%/Rare:%+t GI ;            ! sadat

LEXICON PURI  ! only puri

: EIT ;
+Pl+Par+Use%/Rare:e%+id GI ;  ! purjeid

LEXICON SEEME    ! only kümme, seeme, kööme; like HAARE

+Sg+Nom:%{W%} GI ;                      ! seeme
+Sg+Par:%{W%}%+t GI ;                   ! seemet
:ne 2C_EXCL_SG_PAR ;                    ! seemne etc

! only if the plural partitive with -id is more frequent than the one with sid (puid-puusid),
! then the word may have vowel plural forms

LEXICON TALV  ! only talv

:v%+%{pl.i%}%{rare%} EIT ;
+Sg+Nom%+Use%/Rare:i GI ;  ! tali

LEXICON PÕLV  ! only põlv

:v%+%{pl.i%} EIT ;
+Sg+Nom%+Use%/Rare:i GI ;  ! tali
%+Use%/Rare:v%+i N_VOWEL_PL_SUBPARA ;  ! põlvil etc
+Pl+Trm%+Use%/Rare:v%+i%+ni GI ;  ! põlvini

LEXICON PUU ! 11 words (kuu, maa, etc) that have possible vowel plural forms 

: ARMEE ;
%+Use%/Rare:%+i N_VOWEL_PL_SUBPARA ;       ! puil etc


LEXICON SÜDA                   ! only süda; like habe

+Sg+Nom: GI ;                          ! süda
+Sg+Par:n%+t GI ;                       ! südant
:me 2C_EXCL_SG_PAR ;                   ! südame etc

LEXICON TEINE   ! only teine; almost = NAINE; much like OLULINE
                  
+Sg+Nom: GI ;
: NE_S ;
+Sg+Ill:E1 GI ;                          ! teise (should be 3rd grade... )
+Sg+Ill%+Use%/Rare:E1%+sse GI ;             ! teisesse
+Pl+Par:%+I1 GI ;                           ! teisi
%+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;       ! teisil etc

LEXICON TUHAT      ! only tuhat; like LOHAKAS

+Sg+Nom:t GI ;                          ! tuhat
:nde 2C ;                               ! tuhande etc
! ????  +Sg+Par:%+t GI ;                              ! tuhat

LEXICON VAHER    ! VVS 3 unstable (closed) type; strengthening gradation
                 ! only vaher, aher
+Sg+Nom:%{W%} GI ;                     ! vaher
:a 2C ;                                ! vahtra etc

LEXICON VÄIKE  ! only väike(ne)  VVS 10

: XÄIKE ;
+Pl+Par%+Use%/Rare:kese%+id GI ;            ! väikeseid
+Pl+Par:kse%+id GI ;                        ! väikseid

LEXICON XÄIKE  ! VVS 10 common part of väike(ne), päike(ne), äike(ne)

+Sg+Nom:ke GI ;
+Sg+Nom%+Use%/Rare:kene GI ;
:kene NE_S ;
%+Use%/Rare:kse N_SG_SUBPARA ;                ! xäikse etc 
+Sg+Par%+Use%/Rare:kse%+t GI ;                ! xäikset
+Sg+Ill:kese%+sse GI ;                        ! xäikesesse
+Sg+Ill%+Use%/Rare:kse%+sse GI ;              ! xäiksesse
+Sg+Ill%+Use%/Rare:ke%+sse GI ;               ! xäikesse (haplology)
%+Use%/Rare:kse%+te N_PL_SUBPARA ;       
+Pl+Par%+Use%/Rare:kes%+i GI ;                ! xäikesi
%+Use%/Rare:kes%+i N_VOWEL_PL_SUBPARA ;       ! xäikesil etc
%+Use%/Rare:kese%+i N_VOWEL_PL_SUBPARA ;      ! xäikeseil etc
%+Use%/Rare:kse%+i N_VOWEL_PL_SUBPARA ;       ! xäikseil etc

LEXICON ÕLU                  ! only õlu

+Sg+Nom:u GI ;                                ! õlu
:le N_SG_SUBPARA ;                             ! õlle etc 
+Sg+Par:u%+t GI ;                             ! õlut
+Sg+Ill:le%+sse GI ;                           ! õllesse 
:le%+de N_PL_SUBPARA ;                         ! õllede etc 
+Pl+Par:le%+sid GI ;                              ! õlusid
+Pl+Par%+Use%/Rare:u%+sid GI ;                              ! õlusid


!---------------
! not-so-exceptional inflectional types
!---------------

! VVS 1
LEXICON AASTA    ! default? for vowel-final; stress on penultimate syllable words

+Sg+Nom:%{no.apo%} GI ;             ! aasta
: 2C ;                              ! aasta etc

! VVS 2
LEXICON REDEL    ! VVS 2 default for consonant-ending, stress on penultimate syllable words
   
+Sg+Nom:%{no.apo%} GI ;             ! redel, koppel
:I5 2C ;                            ! redeli etc

! VVS 2
LEXICON VIRSIK    ! VVS 2 default for -ik, -ing; stress on penultimate syllable words

+Sg+Nom:%{no.apo%} GI ;              ! virsik
:u 2C ;                              ! virsiku etc

! VVS 2
LEXICON LOHAKAS      ! VVS 2 -kas, -jas etc ending words (incl derivations)

+Sg+Nom:s GI ;                       ! lohakas
: 2C ;                               ! lohaka etc

! VVS 2
LEXICON TUNTUD      ! VVS 2 -d ending words (incl derivations)

+Sg+Nom:d GI ;                       ! tuntud
: 2C ;                               ! tuntu etc
 
! VVS 2
LEXICON MAGUS    ! VVS 2 unstable (closed) type

+Sg+Nom: GI ;                         ! magus
:A1 2C ;                               ! magusa

! VVS 2
LEXICON HALE    ! VVS 2 unstable (closed) type

+Sg+Nom: GI ;                         ! hale, sajas
:da 2C ;                              ! haleda, sajanda


! VVS 3
LEXICON HOOLAS    ! VVS 3 unstable (closed) type; strengthening gradation

+Sg+Nom:%{W%} GI ;                           ! hoolas
:a 2C ;                                      ! hoolsa etc
+Sg+Par%+Use%/Rare:%{W%}%+t GI ;             ! hoolast
%+Use%/NotNorm:%{W%}%+te N_PL_SUBPARA ;      ! hoolaste etc 

! VVS 4
LEXICON HABE    ! VVS 4 unstable (closed) type; almost like HAARE, but no gradation

+Sg+Nom: GI ;                          ! habe
+Sg+Par:%+t GI ;                       ! habet
:me 2C_EXCL_SG_PAR ;                   ! habeme etc

! VVS 5
LEXICON HAARE    ! VVS 5 unstable (closed) type

+Sg+Nom:%{W%} GI ;                      ! haare
+Sg+Par:%{W%}%+t GI ;                   ! haaret
:me 2C_EXCL_SG_PAR ;                    ! haarme etc

! VVS 6
LEXICON PINGE    ! VVS 6 stable (open) + unstable (closed) type; 
! open to verbal derivations, e.g. kiune from kiunuma

+Sg+Nom:%{W%} GI ;                      ! pinge
+Sg+Par:%{W%}%+t GI ;                   ! pinget
:%{W%}%+te N_PL_SUBPARA ;               ! pingete etc 
: 2C_STRONG ;                           ! pinge etc, pingeid, pingeil etc

! VVS 7
LEXICON KOTKAS    !  VVS 7 unstable (closed) type; 

+Sg+Nom:s%{W%} GI ;                      ! kotkas
+Sg+Par:s%{W%}%+t GI ;                   ! kotkast
:s%{W%}%+te N_PL_SUBPARA ;               ! kotkaste etc 
: 2C_STRONG ;                            ! kotka etc, kotkaid, kotkail etc

! VVS 8
LEXICON PIPAR    !  VVS 8 unstable (closed) type; = HOOLAS

+Sg+Nom:%{W%} GI ;                       ! pipar
+Sg+Par:%{W%}%+t GI ;                    ! pipart
:%{W%}%+de N_PL_SUBPARA ;                ! piparde etc 
%+Use%/NotNorm:a%+te N_PL_SUBPARA ;      ! piprate etc 
:a 2C_STRONG ;

! VVS 8
LEXICON KANNEL    ! VVS 8 unstable (closed) type; = PIPAR, but stem vowel -e instead of -a

+Sg+Nom:%{W%} GI ;                        ! kannel, tütar
+Sg+Par:%{W%}%+t GI ;                     ! kannelt, tütart
:%{W%}%+de N_PL_SUBPARA ;                 ! kannelde, tütarde etc 
%+Use%/NotNorm:e%+te N_PL_SUBPARA ;       ! kandlete, tütrete etc 
:e 2C_STRONG ;

! VVS 9, 10
LEXICON SOOLANE  ! VVS 9 simplex words ending with -s (katus), 10 affix -ne; 
                       
+Sg+Nom: GI ;
: NE_S ;
+Sg+Ill:E1%+sse GI ;                         ! soolasesse, katusesse
+Pl+Par:E1%+id GI ;                          ! soolaseid, katuseid
%+Use%/Rare:E1%+i N_VOWEL_PL_SUBPARA ;       ! soolaseil, katuseil etc

! VVS 11, 12
LEXICON OLULINE   ! affixes -lane, -line, -mine, -tine, -ke(ne), -stikune (?)
                  ! derived words ending with -us, -is (?) (harjutus)
                  
+Sg+Nom: GI ;
: NE_S ;
+Sg+Ill%+Use%/Rare:E1%+sse GI ;             ! olulisesse, harjutusesse
+Sg+Ill:E1%+se GI ;                          ! olulisse, harjutusse (haplology)
+Pl+Par:%+I1 GI ;                           ! olulisi, harjutusi
%+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;       ! olulisil, harjutusil etc

! VVS 9 & (11), 10 & (12)
LEXICON KATKINE  ! affix -ne, but sg ill, pl part and vocal cases also like -line
                 ! simplex -s, but also like -us (gloobus) 
                 
+Sg+Nom: GI ;
: SOOLANE ;
+Sg+Ill%+Use%/Rare:E1%+se GI ;               ! katkisse, gloobusse (haplology)
+Pl+Par%+Use%/Rare:%+I1 GI ;                ! katkisi, gloobusi
%+Use%/Rare:%+I1 N_VOWEL_PL_SUBPARA ;       ! katkisil etc; (ever used?)


! VVS (9) & 11, (10) & 12
LEXICON SUULINE  ! affix -line, but pl part and vocal cases also like -ne
                 ! derived -us, but pl part and vocal cases (?) also like simplex -s (vaatlus) 

+Sg+Nom: GI ;
: OLULINE ;
+Pl+Par%+Use%/Rare:E1%+id GI ;               ! suuliseid, vaatluseid
%+Use%/Rare:E1%+i N_VOWEL_PL_SUBPARA ;       ! suuliseil, vaatluseil etc

! VVS 12
LEXICON PEENIKE   ! affix -ke; almost = OLULINE, except sg nom

+Sg+Nom: GI ;
+Sg+Nom%+Use%/Rare:ne GI ;
:ne NE_S ;
+Sg+Ill%+Use%/Rare:se%+sse GI ;              ! olulisesse, harjutusesse
+Sg+Ill:s%+se GI ;                           ! olulisse, harjutusse (haplology)
+Pl+Par:s%+i GI ;                            ! olulisi, harjutusi
%+Use%/Rare:s%+i N_VOWEL_PL_SUBPARA ;       ! vocal pl cases  olulisil, harjutusil

!  VVS 13
LEXICON KEEL    !  VVS 13 unstable (closed) type; 

: VARS ;
%+Use%/Hyp:%{W%}%+i N_VOWEL_PL_SUBPARA ;       ! vocal pl cases  keelil; except for some expressions (suuril silmil)

!  VVS 14 unstable (closed) type;
LEXICON VARS     !  VVS 14 unstable (closed) type;

! %{W%} here helps to select the right paradigm slots for stem change;
! it does not really stand for "weak grade"

+Sg+Nom: GI ;                                 ! vars
:e%{W%} N_SG_SUBPARA ;                        ! varre etc 
+Sg+Par:%+t GI ;                              ! vart
+Sg+Ill:%{S%}%+de SG_ILL ;              ! varde
+Sg+Ill:e%{W%} SG_ILL ;          ! varresse 
:%+te N_PL_SUBPARA ;                          ! varte etc 
+Pl+Par:%+i GI ;                              ! varsi
! +N%+Use%/Hyp:%+i N_VOWEL_PL_SUBPARA ;       ! varsil?; except for some expressions (uusil)

!  VVS 15 unstable (closed) type;
LEXICON MESI     

+Sg+Nom:i GI ;                                ! mesi
:e%{W%} N_SG_SUBPARA ;                        ! mee etc 
+Sg+Par:%+tt GI ;                             ! mett
+Sg+Ill:e%{W%}%+sse GI ;                      ! meesse
:%{W%}%+te N_PL_SUBPARA ;                     ! mete etc 
+Pl+Par:i GI ;                                ! mesi ?? meesid ???
! +N%+Use%/Hyp: N_VOWEL_PL_SUBPARA ;          ! mesil? etc; except for some expressions (käsil) ?

! VVS 16
LEXICON PERE

+Sg+Nom:%{no.apo%} GI ;
: N_SG_SUBPARA ;      
+Sg+Par:%+t GI ;                           ! peret
+Sg+Ill:%{S%} SG_ILL ;               ! perre
+Sg+Ill:%{W%} SG_ILL ;        ! peresse 
:%+de N_PL_SUBPARA ;                       ! perede etc
+Pl+Par:%+sid #;                           ! peresid

! %{W%} here helps to select the right paradigm slots for stem change;
! it does not really stand for "weak grade"
! VVS 17, 18
LEXICON KAVA

+Sg+Nom:%{no.apo%} GI ;
:%{W%} N_SG_SUBPARA ;                      ! kava etc
+Sg+Par: GI ;                              ! kava
+Sg+Ill:%{S%} SG_ILL ;               ! kavva
+Sg+Ill:%{W%} SG_ILL ;        ! kavasse 
:%+de N_PL_SUBPARA ;                       ! kavade etc
+Pl+Par:%+%{s%}%{i%}%{d%} PL_PAR ;       ! kavasid & kavu ...

! VVS 2 & 19 
LEXICON MUUSEUM  ! undecided which stable type (aktiivne muuttüüp): TAUD or REDEL 

+Sg+Nom:%{no.apo%} GI ;
:i%{W%} N_SG_SUBPARA ; 
+Sg+Par:i GI ;                              ! muuseumi
+Sg+Ill:i GI ;                              ! muuseumi  ! ?? not sure; rare ??
+Sg+Ill%+Use%/Rare:i%{W%}%+sse GI ;         ! muuseumisse ?? not sure; rare ??
:i%+de N_PL_SUBPARA ;                       ! muuseumide etc 
+Pl+Par:e GI ;                              ! muuseume

+Sg+Par%+Use%/Rare:i%+t GI ;                ! muuseumit
%+Use%/Rare:i%+te N_PL_SUBPARA ;            ! muuseumite etc 
+Pl+Par%+Use%/Rare:e%+id GI ;               ! muuseumeid
%+Use%/Rare:e%+i N_VOWEL_PL_SUBPARA ;       ! muuseumeil etc

! VVS 20
LEXICON MERI   ! unstable type (passiivne muuttüüp) lumi, tuli, uni, veri

+Sg+Nom:i GI ;
:e N_SG_SUBPARA ;                        ! mere etc
+Sg+Par:%+d GI ;                         ! merd
+Sg+Ill:e%{S%} SG_ILL ;            ! merre
+Sg+Ill:e%{W%} SG_ILL ;     ! meresse 
:%+e%+de N_PL_SUBPARA ;                  ! merede etc
+Pl+Par:e%+sid GI ;                      ! meresid

! VVS 20, 21
LEXICON NIMI   ! unstable type (passiivne muuttüüp) suvi, lagi

+Sg+Nom:i GI ;
:e%{W%} N_SG_SUBPARA ;                   ! suve etc
+Sg+Par:e GI ;                           ! suve, lage
+Sg+Ill:e%{S%} SG_ILL ;            ! nimme
+Sg+Ill:e%{W%} SG_ILL ;     ! nimesse 
:%+e%+de N_PL_SUBPARA ;                  ! lagede etc
+Pl+Par:e%+sid GI ;                      ! lagesid

! VVS 22 
LEXICON PIIM   ! unstable type (passiivne muuttüüp) sepp, kaup, ...

+Sg+Nom:%{no.apo%} GI ;
:%{sg.a%} 1C ;

! VVS 22 
LEXICON KOON   ! unstable type (passiivne muuttüüp) lõpp, laug

+Sg+Nom:%{no.apo%} GI ;
:%{sg.u%} 1C ;

! VVS 22 
LEXICON EIT   ! unstable type (passiivne muuttüüp) kuusk, kärg

+Sg+Nom:%{no.apo%} GI ;
:%{sg.e%} 1C ;

! VVS 22 
LEXICON TAUD  ! stable type (aktiivne muuttüüp) siil, president, ...

+Sg+Nom:%{no.apo%} GI ;
:%+%{pl.e%}%{sg.i%} 1C ;  ! {pl.e} is here so it need not be in the stem lexicon

! VVS 22 
LEXICON FIAT          ! stable type (aktiivne muuttüüp) almost == TAUD, but final stop weak grade does not change 

+Sg+Nom:%{no.apo%} GI ;
:i%{WF%} N_SG_SUBPARA ;                ! Fiati etc
+Sg+Par:i GI ;                         ! Fiati
+Sg+Ill:i GI ;                         ! Fiati
+Sg+Ill%+Use%/Hyp:i%{WF%}%+sse GI ;    ! Fiatisse
:%+%{pl.e%}%{sg.i%} 1C_PL ;            ! Fiatide, Fiate, Fiatisid+Use/Rare 

! VVS 22 
LEXICON KABINET                 ! stable type (aktiivne muuttüüp) almost == TAUD

+Sg+Nom:%{no.apo%} GI ;
                                      ! sg nom has a single final stop instead of double, 
                                      ! so it has to be doubled in strong grade
:%.%+%{pl.e%}%{sg.i%} 1C ;

! VVS 23 
LEXICON KAEL   ! almost = PIIM, except pl gen + 

+Sg+Nom:%{no.apo%} GI ;
:%{sg.a%} 1C_SG ;
:%{W%}%+te N_PL_SUBPARA ;                     ! kaelte etc 
%+Use%/Rare:%{sg.a%}%+de N_PL_SUBPARA ;       ! kaelade etc 
                                              ! pl par stem vowel (u, i, 0) is lexically determined 
                                              ! and usage-wise tagged
+Pl+Par:%{sg.a%}%+%{s%}%{i%}%{d%} PL_PAR ;  ! kaelu, kaelasid+Use/Rare

! VVS 23 
LEXICON POISS   ! poiss, püks; almost = TAUD, except pl gen + 

+Sg+Nom:%{no.apo%} GI ;
:i 1C_SG ;                                  ! poisi etc
:%{W%}%+te N_PL_SUBPARA ;                   ! poiste etc
%+Use%/Rare:%+i%+de N_PL_SUBPARA ;          ! poisside etc
+Pl+Par:e GI ;                              ! poisse
+Pl+Par%+Use%/Hyp:i%+sid GI ;              ! poissisid+Use/Hyp

! VVS 24 is lexical variants under VVS 22 various subtypes

! VVS 25 
LEXICON ELANIK          ! stable type (aktiivne muuttüüp) almost == KABINET == TAUD

+Sg+Nom:%{no.apo%} GI ;                        ! elanik
                                               ! sg nom has a single final stop instead of double, 
                                               ! so it has to be doubled in strong grade
:%.%{sg.u%} 1C_SG ;                            ! elaniku etc
:%.e%{W%}%+ N_PL_SUBPARA ;                     ! NB! elanike etc
%+Use%/Rare:%.%{sg.u%}%+de N_PL_SUBPARA ;      ! elanikkude etc   
+Pl+Par:%.e GI ;                               ! elanikke
+Pl+Par%+Use%/Rare:%.%{sg.u%}%+sid GI ;        ! elanikkusid+Use/Rare

LEXICON ARMEE ! VVS 26

+Sg+Nom:%{no.apo%} GI ;                        ! armee
: N_SG_SUBPARA ;                               ! armee etc
+Sg+Par:%+d GI ;                               ! armeed
+Sg+Ill:%{W%} SG_ILL ;                       ! armeesse, öösse; {W} is here just to get SG_ILL right
+Sg+Ill:%{S%} SG_ILL ;                       ! öhe, suhu, sohu, pähe
:%+de N_PL_SUBPARA ;                           ! armeede etc  
+Pl+Par:%+%{s%}id PL_PAR ;                 ! armeesid; armeid...

! ---------------
! sub-paradigms common to several inflectional types
! ---------------

! VVS 9, 10, 11, 12
LEXICON NE_S  ! VVS 9, 10, 11, 12 common forms for affix -ne, -line, -s, -tus etc 

:E1 N_SG_SUBPARA ;                
+Sg+Par:E1%+t GI ;                        ! soolast, raudset, olulist
:E1%+te N_PL_SUBPARA ;        

! ------

LEXICON 2C ! consonant ending with stress on penultimate syllable, or vowel ending 

: 2C_EXCL_SG_PAR ;                        ! redeli, virsiku  
+Sg+Par:%+t GI ;                          ! redelit, virsikut

LEXICON 2C_EXCL_SG_PAR ! =2C, excluding sg partitive

:%+te N_PL_SUBPARA ;                       ! redelite etc
: 2C_STRONG ;                              ! redeleid, redeleil etc

LEXICON 2C_STRONG ! =2C_EXCL_SG_PAR, excluding N_PL_SUBPARA

: N_SG_SUBPARA ;                           ! redeli, kotka etc
+Sg+Ill: SG_ILL ;                ! taskusse or tasku
+Pl+Par:%+id GI ;                          ! kotkaid
%+Use%/Rare:%+i N_VOWEL_PL_SUBPARA ;       ! kotkail etc

LEXICON 1C ! consonant ending, stress NOT on penultimate syllable

: 1C_SG ;  ! singular forms + pl nom
: 1C_PL ;  ! plural forms

LEXICON 1C_SG ! SG subpara of 1C

:%{W%} N_SG_SUBPARA ;                      ! koti etc
+Sg+Par: GI ;                              ! kotti
+Sg+Ill: GI ;                              ! kotti
+Sg+Ill%+Use%/Hyp:%{W%}%+sse GI ;          ! kotisse

LEXICON 1C_PL ! PL subpara of 1C

:%+de N_PL_SUBPARA ;                       ! kottide etc
                                           ! pl par stem vowel (u, i, 0) is lexically determined 
                                           ! and usage-wise tagged
+Pl+Par:%+%{s%}%{i%}%{d%} PL_PAR ;       ! kingi, kingasid+Use/Rare vs. puudi+Use/Rare, puudasid

! ---------------
! most regular ...
! ---------------

LEXICON N_SG_SUBPARA     ! sg gen + sg cases depending on it + pl nom; excl sg ill

+Pl+Nom:%+d GI ;
+Sg: REGULAR_GEN_ET_AL ;

LEXICON N_PL_SUBPARA     ! pl gen + pl cases depending on it 

+Pl: STANDARD_GEN_ET_AL ;

LEXICON STANDARD_GEN_ET_AL  ! genitive and cases depending on it (either sg or pl)
                            ! incl. sse-illative 
+Gen: GI ; 
: STANDARD_SEM_CASES ;

LEXICON REGULAR_GEN_ET_AL  ! genitive and cases depending on it (either sg or pl)
                            ! NB! excl. illative 
+Gen: GI ; 
: REGULAR_SEM_CASES ;

LEXICON STANDARD_SEM_CASES     ! semantic cases, incl sse-illative

+Ill:%+sse GI ;
: REGULAR_SEM_CASES ;

LEXICON REGULAR_SEM_CASES     ! semantic cases, excl illative

: REGULAR_LOCATIVES ;
+Tra:%+ks GI ;
: NINATAGA ;

LEXICON NINATAGA

+Trm:%+ni GI ;	
+Ess:%+na GI ;	
+Abe:%+ta GI ;	
+Com:%+ga GI ;

	
LEXICON N_VOWEL_PL_SUBPARA     ! pl cases depending on vowel plural gen  
                               ! excl. ni-na-ta-ga 
+Pl: LOCATIVES ;

LEXICON LOCATIVES            

: STANDARD_LOCATIVES ;
+Tra:%+ks GI ;

LEXICON STANDARD_LOCATIVES            

+Ill:%+sse GI ;
: REGULAR_LOCATIVES ;

LEXICON REGULAR_LOCATIVES            

+Ine:%+s GI ;
+Ela:%+st GI ;
+All:%+le GI ;	
+Ade:%+l GI ;	
+Abl:%+lt GI ;	

! we want to indicate the preferred form in the stem lexicon
! (not in the continuation lexicon)

LEXICON PL_PAR                       ! for TAUD, PIIM, KOON etc types

%+Use%/Rare:%{rare%} GI ;            ! less used pl par form for that word
 GI ;                                ! default pl par form for that word

LEXICON SG_ILL                       ! for KEEL etc types

%+Use%/Rare:%+%{s%}%{s%}%{e%}%{rare%} GI ;  ! less used sg ill form for that word
:%+%{s%}%{s%}%{e%} GI ;                     ! default sg ill form for that word

